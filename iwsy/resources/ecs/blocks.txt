!	Blocks

	script Blocks
    
    import div Panel and variable Presentation
    
    div Editor
    div Title
    div Table
    div Row
    div Cell
    div Empty
    input PropertyValue
    button EditButton
    button SaveButton
    img ShowBlock
    img AddBlock
    img Up
    img DeleteBlock
    a Link
    variable Blocks
    variable Block
    variable Block2
    variable PropertyDefaults
    variable PropertyNames
    variable PropertyName
    variable SelectedBlock
    variable SavedIndex
    variable Message
    variable N
    variable NBlocks
    
    put -1 into SelectedBlock
    
    rest get PropertyNames from `/resources/json/propertyNames.json`
    rest get PropertyDefaults from `/resources/json/propertyDefaults.json`
    
    on message go to Start
    set ready
    stop

Start:
	if the message is `save`
    begin
    	gosub to SaveSelectedBlock
        stop
    end

Restart:
	put property `blocks` of Presentation into Blocks
	clear Panel
    create Row in Panel
    set the style of Row to `display:flex`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center;margin-bottom:0.5em`
    set the content of Title to `Blocks`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddBlock in Link
    set the style of AddBlock to `width:1em;margin-top:0.1em`
    set attribute `src` of AddBlock to `resources/icon/plus.png`
    on click AddBlock
    begin
        set Block to object
        put 0 into N
        while N is less than the json count of PropertyNames
        begin
        	put element N of PropertyNames into PropertyName
            set property PropertyName of Block to property PropertyName of PropertyDefaults
        	add 1 to N
        end
        json add Block to Blocks
        set property `blocks` of Presentation to Blocks
        go to Restart
    end

	put the json count of Blocks into NBlocks
    set the elements of ShowBlock to NBlocks
    set the elements of EditButton to NBlocks
    set the elements of SaveButton to NBlocks
    set the elements of Editor to NBlocks
    set the elements of Up to NBlocks
    set the elements of DeleteBlock to NBlocks
    create Table in Panel
    set the style of Table to `width:100%`
    put 0 into N
    while N is less than NBlocks
    begin
        index EditButton to N
        index Up to N
        index DeleteBlock to N
        index Editor to N
        create Row in Table
        create Cell in Row
        set the style of Cell to `width:100%;display:flex`
        create Link in Cell
    	index ShowBlock to N
        create ShowBlock in Link
        set the style of ShowBlock to `width:1em;margin:0.1em 0.2em 0 0`
        set attribute `src` of ShowBlock to `resources/icon/binoculars.png`
        create EditButton in Cell
        set the style of EditButton to `flex:1`
    	put element N of Blocks into Block
        set the text of EditButton to property `name` of Block
        if N is 0
        begin
            create Empty in Cell
            set the style of Empty to `width:1em;margin:0.2em 0 0 0.2em`
        end
        else
        begin
            create Link in Cell
            create Up in Link
            set the style of Up to `width:1em;margin:0.2em 0 0 0.2em`
            set attribute `src` of Up to `/resources/icon/up.png`
        end
        create Link in Cell
        create DeleteBlock in Link
        set the style of DeleteBlock to `width:1em;margin:0.2em 0 0 0.2em`
        set attribute `src` of DeleteBlock to `/resources/icon/stop.png`
        create Editor in Row
        set the style of Editor to `margin:0.5em;border:1px solid black;padding:0.2em;display:none`
    	add 1 to N
    end
    on click ShowBlock
    begin
    	set Message to object
        set property `action` of Message to `block`
        set property `block` of Message to the index of ShowBlock
        send Message to parent
    end
    on click EditButton
    begin
        put the index of EditButton into SavedIndex
        gosub to SaveSelectedBlock
        put SavedIndex into SelectedBlock
        put 0 into N
        while N is less than NBlocks
        begin
        	if N is not SelectedBlock
            begin
                index EditButton to N
                set style `background` of EditButton to ``
                index Editor to N
                set style `display` of Editor to `none`
                clear Editor
            end
            add 1 to N
        end
        index EditButton to SelectedBlock
        index Editor to SelectedBlock
        index SaveButton to SelectedBlock
		if style `display` of Editor is `none`
        begin
        	set style `background` of EditButton to `lightgray`
            set style `display` of Editor to `block`
            put element SelectedBlock of Blocks into Block
            put the json count of PropertyNames into N
            set the elements of PropertyValue to N
            put 0 into N
            while N is less than the json count of PropertyNames
            begin
                create Row in Editor
                set the style of Row to `display:flex`
                put element N of PropertyNames into PropertyName
                create Cell in Row
                set the style of Cell to `width:8em;padding-left:0.5em`
                set the content of Cell to PropertyName
                index PropertyValue to N
                create PropertyValue in Row
                set the style of PropertyValue to `flex:1`
                set the text of PropertyValue to property PropertyName of Block
                add 1 to N
            end
            create Row in Editor
            create SaveButton in Row
            set the style of SaveButton to `width:100%`
            set the text of SaveButton to `Save`
            on click SaveButton gosub to SaveSelectedBlock
        end
        else
        begin
            clear Editor
            set style `display` of Editor to `none`
        end
    end
    on click Up
    begin
    	put the index of Up into N
        put element N of Blocks into Block
        take 1 from N
        put element N of Blocks into Block2
        set element N of Blocks to Block
        add 1 to N
        set element N of Blocks to Block2
        set property `blocks` of Presentation to Blocks
        go to Restart
    end
    on click DeleteBlock
    begin
    	put the index of DeleteBlock into N
        json delete element N of Blocks
        set property `blocks` of Presentation to Blocks
        go to Restart
    end
	stop

!	Save the seleced block
SaveSelectedBlock:
	if SelectedBlock is -1 return
    index EditButton to SelectedBlock
    put element SelectedBlock of Blocks into Block
	put 0 into N
    while N is less than the json count of PropertyNames
    begin
        put element N of PropertyNames into PropertyName
    	index PropertyValue to N
        set property PropertyName of Block to the text of PropertyValue
    	add 1 to N
    end
    set the text of EditButton to property `name` of Block
    set element SelectedBlock of Blocks to Block
    set property `blocks` of Presentation to Blocks

!	Tell the parent to refresh the script
    set Message to object
    set property `action` of Message to `refresh`
    set property `source` of Message to `blocks`
    send Message to parent
	return