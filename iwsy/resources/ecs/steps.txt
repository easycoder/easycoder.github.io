!	Steps

	script Steps
    
    import div Panel and variable Presentation
    
    div StepsPanel
    div BlockTable
    div BlockRow
    div BlockData
    div Row
    div Row1
    div Cell
    div Editor
    div Title
    button StepButton
    button Header
    button InsertBefore
    button Save
    button InsertAfter
    table Table
    tr TR
    td TD
    select ActionSelect
    select BlockSelect
    select ContentSelect
    select TargetSelect
    select ContinueSelect
    input InitInput
    input TitleInput
    input LabelInput
    input DurationInput
    input TargetInput
    input URLInput
    img AddStep
    img PlusIcon
    img DeleteIcon
    img DeleteStep
    img ShowStep
    a Link
    variable Steps
    variable SelectedStep
    variable CurrentStep
    variable Action
    variable Blocks
    variable Block
    variable Contents
    variable Target
    variable Types
    variable Duration
    variable URL
    variable Names
    variable Item
    variable ActionNames
    variable InitProperties
    variable InitProperty
    variable BlockNames
    variable ContentNames
    variable ContinueTypes
    variable TrueFalse
    variable Response
    variable Message
    variable Running
    variable N
    variable B

    put -1 into SelectedStep

    create StepsPanel in Panel
	set the style of StepsPanel to `flex:1;overflow-y:scroll`
    
    on message go to Start
    set ready
    stop
    
Start:
    if the message is empty go to Restart

	put the message into Message
    if Message is `save`
    begin
    	if SelectedStep is not -1
        begin
	        gosub to SaveCurrentStep
	        put -1 into SelectedStep
        end
        stop
    end
    if property `action` of Message is `clear`
    begin
    	clear Running
    	put 0 into N
        while N is less than the elements of StepButton
        begin
    	    index StepButton to N
        	set style `background` of StepButton to ``
            add 1 to N
    	end
        stop
    end
    if property `action` of Message is `running`
    begin
    	set Running
    	stop
    end
    if property `action` of Message is `step`
    begin
    	put property `step` of Message into N
        index StepButton to N
        set style `background` of StepButton to `#cfc`
    	stop
    end

Restart:
	set style `display` of Panel to `flex`
	set style `flex-direction` of Panel to `column`
	clear StepsPanel
    create Row in StepsPanel
    set the style of Row to `display:flex`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center;margin-bottom:0.5em`
    set the content of Title to `Steps`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddStep in Link
    set the style of AddStep to `width:1em;margin-top:0.1em`
    set attribute `src` of AddStep to `resources/icon/plus.png`
    on click AddStep go to AddNewStep

	put property `steps` of Presentation into Steps
    put the json count of Steps into N
    set the elements of ShowStep to N
    set the elements of StepButton to N
    set the elements of DeleteStep to N
    set the elements of TitleInput to N
    set the elements of LabelInput to N
    set the elements of DurationInput to N
    set the elements of Editor to N
    put 0 into N
    while N is less than the elements of StepButton
    begin
    	index StepButton to N
    	create Row in StepsPanel
        set the style of Row to `width:100%`
        create Row1 in Row
        set the style of Row1 to `display:flex;margin-bottom:0.2em`
        create Cell in Row1
        set the style of Cell to `width:1.4em;text-align:center`
        create Link in Cell
    	index ShowStep to N
        create ShowStep in Link
        set the style of ShowStep to `width:1em;margin-top:0.1em`
        set attribute `src` of ShowStep to `resources/icon/binoculars.png`
    	create StepButton in Row1
        set the style of StepButton to `flex:1`
        put element N of Steps into CurrentStep
        set the text of StepButton to property `title` of CurrentStep
        create Cell in Row1
        set the style of Cell to `width:1.4em;text-align:center`
        index DeleteStep to N
        create Link in Cell
        create DeleteStep in Link
        set the style of DeleteStep to `width:1em;margin-top:0.1em`
        set attribute `src` of DeleteStep to `resources/icon/stop.png`
        index Editor to N
        create Editor in Row
    	add 1 to N
	end
    on click StepButton
    begin
       	gosub to SaveCurrentStep
    	put SelectedStep into N
        put the index of StepButton into SelectedStep
        if SelectedStep is N
        begin
            index Editor to N
            set style `display` of Editor to `none`
            index StepButton to N
            set style `background` of StepButton to ``
        	gosub to SaveCurrentStep
            put -1 into SelectedStep
        end
        else
        begin
            if N is not -1
            begin
    			gosub to SaveCurrentStep
                put 0 into N
                while N is less than the elements of StepButton
                begin
                    index Editor to N
                    set style `display` of Editor to `none`
                    index StepButton to N
                    set style `background` of StepButton to ``
                    add 1 to N
                end
            end
            index StepButton to SelectedStep
            set style `background` of StepButton to `lightgray`
            index Editor to SelectedStep
            set style `display` of Editor to `block`
            put element SelectedStep of Steps into CurrentStep
            go to EditStep
      	end
    end
    on click DeleteStep
    begin
    	put the index of DeleteStep into N
        json delete element N of Steps
    	set property `steps` of Presentation to Steps
        go to Restart
    end
    on click ShowStep
    begin
    	if Running stop
    	put 0 into N
        while N is less than the json count of Steps
        begin
        	index StepButton to N
        	set style `background` of StepButton to ``
        	add 1 to N
        end
    	put the index of ShowStep into N
        index StepButton to N
        set style `background` of StepButton to `#ccf`
        set Message to object
        set property `action` of Message to `goto`
        set property `index` of Message to N
        send Message to parent
    end

!	Set up the fixed lists
    set ActionNames to array
    json add `init` to ActionNames
    json add `set content` to ActionNames
    json add `show` to ActionNames
    json add `hide` to ActionNames
    json add `pause` to ActionNames
    json add `hold` to ActionNames
    json add `fade up` to ActionNames
    json add `fade down` to ActionNames
    json add `crossfade` to ActionNames
    json add `transition` to ActionNames
    json add `goto` to ActionNames
    json add `load` to ActionNames
    set InitProperties to array
    json add `title` to InitProperties
    json add `aspect ratio` to InitProperties
    json add `background` to InitProperties
    json add `border` to InitProperties
    set ContinueTypes to array
    json add `true` to ContinueTypes
    json add `false` to ContinueTypes
    
    put property `steps` of Presentation into Steps
    clear Running
    
    stop

!	Add a new step to the list
AddNewStep:
	gosub to CreateNewStep
    json add CurrentStep to Steps
    go to ASA3

!	Add a step before the current one
AddStepBefore:
    put the json count of Steps into N
    set CurrentStep to object
    json add CurrentStep to Steps
    go to ASA2

!	Add a step after the current one
AddStepAfter:
    put the json count of Steps into N
    set CurrentStep to object
    json add CurrentStep to Steps
    add 1 to SelectedStep
ASA2:
    while N is greater than SelectedStep
    begin
    	take 1 from N
        put element N of Steps into CurrentStep
        add 1 to N
        set element N of Steps to CurrentStep
        take 1 from N
    end
    gosub to CreateNewStep
    set element N of Steps to CurrentStep
ASA3:
    put -1 into SelectedStep
    set property `steps` of Presentation to Steps
    go to Restart

CreateNewStep:
    set CurrentStep to object
    set property `title` of CurrentStep to `New step`
    set property `action` of CurrentStep to ``
    set property `label` of CurrentStep to ``
	return

!	Edit a single step
EditStep:
	put property `blocks` of Presentation into Blocks
    put property `content` of Presentation into Contents
    put the json keys of Blocks into BlockNames
    put the json keys of Contents into ContentNames
    put element SelectedStep of Steps into CurrentStep

    clear Editor
    create Table in Editor

ReloadStepEditor:
    set the style of Table to `width:100%`
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `Title:`
    create TD in TR
    index TitleInput to SelectedStep
    create TitleInput in TD
    set the style of TitleInput to `width:100%`
    set the content of TitleInput to property `title` of CurrentStep
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `Label:`
    create TD in TR
    index LabelInput to SelectedStep
    create LabelInput in TD
    set the style of LabelInput to `width:100%`
    set the content of LabelInput to property `label` of CurrentStep
    create TR in Table
    create TD in TR
    set the content of TD to `Action:`
    create TD in TR
    create ActionSelect in TD
    set the style of ActionSelect to `width:100%`
    put property `action` of CurrentStep into Action
    set ActionSelect from ActionNames as Action
    if Action is `init` gosub to EditInit
    else if Action is `set content` gosub to EditSetContent
    else if Action is `show` gosub to EditShow
    else if Action is `hide` gosub to EditHide
    else if Action is `pause` gosub to EditPause
    else if Action is `hold` gosub to EditHold
    else if Action is `fade up` gosub to EditFadeUp
    else if Action is `fade down` gosub to EditFadeDown
    else if Action is `crossfade` gosub to EditCrossfade
    else if Action is `transition` gosub to EditTransition
    else if Action is `goto` gosub to EditGoto
    else if Action is `load` gosub to EditLoad
    else
    begin
    end
    on change ActionSelect
    begin
    	set Response
    	if Action is not empty
        begin
            if not confirm `Changing the action will remove all step data. Do you want to continue?`
            	clear Response
        end
        if Response
        begin
        	index StepButton to SelectedStep
            set property `title` of CurrentStep to TitleInput
            put the selected item in ActionSelect into Action
            gosub to CreateNewAction
            clear Table
            go to ReloadStepEditor
        end
        else set ActionSelect from ActionNames as property `action` of CurrentStep
    end
    
!	Add the buttons below the table
    create TR in Table
    create TD in TR
    create TD in TR
    create InsertBefore in TD
    set the style of InsertBefore to `width:calc(100% / 3)`
    set the text of InsertBefore to `Add Step Before`
    on click InsertBefore go to AddStepBefore
    create Save in TD
    set the style of Save to `width:calc(100% / 3)`
    set the text of Save to `Save`
    on click Save gosub to SaveCurrentStep
    create InsertAfter in TD
    set the style of InsertAfter to `width:calc(100% / 3)`
    set the text of InsertAfter to `Add Step After`
    on click InsertAfter go to AddStepAfter
	stop

!	Editors for the various Action types

EditInit:
	create TR in Table
    create TD in TR
    set the content of TD to `Properties:`
    create TD in TR
	create BlockTable in TD
    set the style of BlockTable to `width:100%;border:1px solid gray`
    put the json count of InitProperties into N
    set the elements of InitInput to N
    put 0 into B
    while B is less than N
    begin
	   	put element B of InitProperties into InitProperty
        create BlockRow in BlockTable
        set the style of BlockRow to `display:flex`
        create Cell in BlockRow
        set the style of Cell to `width:6em;padding-left:0.2em`
        set the content of Cell to InitProperty
        index InitInput to B
        create InitInput in BlockRow
        set the style of InitInput to `flex:1`
        put property InitProperty of CurrentStep into Item
        replace `&quot;` with `"` in Item
        set the content of InitInput to Item
    	add 1 to B
    end
	return

EditSetContent:
	create TR in Table
    create TD in TR
    set the content of TD to `Blocks:`
    create TD in TR
	create BlockTable in TD
    set the style of BlockTable to `width:100%;border:1px solid gray`
    create BlockRow in BlockTable
    set the style of BlockRow to `display:flex`
    create Header in BlockRow
    set the style of Header to `flex:1`
    set the content of Header to `Blocks`
    create Header in BlockRow
    set the style of Header to `flex:1`
    set the content of Header to `Content`
    create BlockData in BlockRow
    set the style of BlockData to `width:2em;text-align:center`
    create Link in BlockData
    create PlusIcon in Link
    set the style of PlusIcon to `width:1em;margin-top:0.1em`
    set attribute `src` of PlusIcon to `resources/icon/plus.png`
    on click PlusIcon
    begin
    	set Block to object
        set property `block` of Block to empty
        set property `content` of Block to empty
        json add Block to Blocks
        set property `blocks` of CurrentStep to Blocks
        clear Table
        go to ReloadStepEditor
    end
    put property `blocks` of CurrentStep into Blocks
    put the json count of Blocks into N
    set the elements of BlockSelect to N
    set the elements of ContentSelect to N
    set the elements of DeleteIcon to N
	put 0 into B
	while B is less than N
	begin
	   	put element B of Blocks into Block
        create BlockRow in BlockTable
        set the style of BlockRow to `display:flex`
        index BlockSelect to B
        create BlockSelect in BlockRow
        set the style of BlockSelect to `flex:1`
        set BlockSelect from BlockNames as property `block` of Block
        index ContentSelect to B
        create ContentSelect in BlockRow
        set the style of ContentSelect to `flex:1`
        set ContentSelect from ContentNames as property `content` of Block
        create BlockData in BlockRow
        set the style of BlockData to `width:2em;text-align:center`
        index DeleteIcon to B
        create Link in BlockData
        create DeleteIcon in Link
        set the style of DeleteIcon to `width:1em;margin-top:0.1em`
        set attribute `src` of DeleteIcon to `resources/icon/stop.png`
        add 1 to B
    end
    on change BlockSelect
    begin
    	put the index of BlockSelect into N
        put element N of Blocks into Block
        set property `block` of Block to BlockSelect
        set element N of Blocks to Block
        set property `blocks` of CurrentStep to Blocks
        set element SelectedStep of Steps to CurrentStep
    end
    on change ContentSelect
    begin
    	put the index of ContentSelect into N
        put element N of Blocks into Block
        set property `content` of Block to ContentSelect
        set element N of Blocks to Block
        set property `blocks` of CurrentStep to Blocks
        set element SelectedStep of Steps to CurrentStep
    end
    on click DeleteIcon
    begin
    	put the index of DeleteIcon into N
        json delete element N of Blocks
        set property `blocks` of CurrentStep to Blocks
        clear Table
        go to ReloadStepEditor
    end
	return

EditShow:
EditHide:
	gosub to EditBlockList
	return

EditPause:
EditHold:
    gosub to EditDuration
	return

EditFadeUp:
EditFadeDown:
	gosub to EditBlockList
    gosub to EditDuration
    gosub to EditContinue
	return

EditCrossfade:
	put ContentNames into Names
    gosub to EditBlockAndTarget
	return

EditTransition:
	put BlockNames into Names
    gosub to EditBlockAndTarget
	return

EditGoto:
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `Target:`
    create TD in TR
    create TargetInput in TD
    set the style of TargetInput to `width:100%`
    set the content of TargetInput to property `target` of CurrentStep
	return

EditLoad:
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `URL:`
    create TD in TR
    create URLInput in TD
    set the style of URLInput to `width:100%`
    set the content of URLInput to property `url` of CurrentStep
	return

EditBlockList:
	create TR in Table
    create TD in TR
    set the content of TD to `Blocks:`
    create TD in TR
	create BlockTable in TD
    set the style of BlockTable to `width:100%;border:1px solid gray`
    create BlockRow in BlockTable
    set the style of BlockRow to `display:flex`
    create Header in BlockRow
    set the style of Header to `flex:1`
    set the content of Header to `Blocks`
    create BlockData in BlockRow
    set the style of BlockData to `width:2em;text-align:center`
    create Link in BlockData
    create PlusIcon in Link
    set the style of PlusIcon to `width:1em;margin-top:0.1em`
    set attribute `src` of PlusIcon to `resources/icon/plus.png`
    on click PlusIcon
    begin
        json add empty to Blocks
        set property `blocks` of CurrentStep to Blocks
        clear Table
        go to ReloadStepEditor
    end
    put property `blocks` of CurrentStep into Blocks
    put the json count of Blocks into N
    set the elements of BlockSelect to N
    set the elements of DeleteIcon to N
    put 0 into B
    while B is less than N
    begin
	   	put element B of Blocks into Block
        create BlockRow in BlockTable
        set the style of BlockRow to `display:flex`
        index BlockSelect to B
        create BlockSelect in BlockRow
        set the style of BlockSelect to `flex:1`
        set BlockSelect from BlockNames as Block
        create BlockData in BlockRow
        set the style of BlockData to `width:2em;text-align:center`
        index DeleteIcon to B
        create Link in BlockData
        create DeleteIcon in Link
        set the style of DeleteIcon to `width:1em;margin-top:0.1em`
        set attribute `src` of DeleteIcon to `resources/icon/stop.png`
    	add 1 to B
    end
    on change BlockSelect
    begin
    	put the index of BlockSelect into N
        set element N of Blocks to the selected item in BlockSelect
        set property `blocks` of CurrentStep to Blocks
        set element SelectedStep of Steps to CurrentStep
    end
    on click DeleteIcon
    begin
    	put the index of DeleteIcon into N
        json delete element N of Blocks
        set property `blocks` of CurrentStep to Blocks
        clear Table
        go to ReloadStepEditor
    end
    return

EditBlockAndTarget:
	create TR in Table
    create TD in TR
    set the content of TD to `Block:`
    create TD in TR
    set the elements of BlockSelect to 1
    put property `block` of CurrentStep into Block
    create BlockRow in TD
    create BlockSelect in BlockRow
    set the style of BlockSelect to `width:100%`
    set BlockSelect from BlockNames as Block
	create TR in Table
    create TD in TR
    set the content of TD to `Target:`
    create TD in TR
    put property `target` of CurrentStep into Target
    create BlockRow in TD
    create TargetSelect in BlockRow
    set the style of TargetSelect to `width:100%`
    set TargetSelect from Names as Target
    gosub to EditDuration
    gosub to EditContinue
	return

EditDuration:
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `Duration:`
    create TD in TR
    index DurationInput to SelectedStep
    create DurationInput in TD
    set the style of DurationInput to `width:100%`
    set the content of DurationInput to property `duration` of CurrentStep
	return

EditContinue:
    create TR in Table
    create TD in TR
    set the style of TD to `width:6em`
    set the content of TD to `Continue:`
    create TD in TR
    create ContinueSelect in TD
    set the style of ContinueSelect to `width:100%`
    put property `continue` of CurrentStep into TrueFalse
    if TrueFalse put `true` into TrueFalse else put `false` into TrueFalse
    set ContinueSelect from ContinueTypes as TrueFalse
	return

!	Create a new action with a full set of empty properties
CreateNewAction:
	index TitleInput to SelectedStep
    if the text of TitleInput is empty
    begin
    	set property `title` of CurrentStep to `New step: ` cat Action
    end
    set property `action` of CurrentStep to Action
    set property `label` of CurrentStep to empty
    if Action is `init`
    begin
    	put 0 into N
        while N is less than the json count of InitProperties
        begin
        	put element N of InitProperties into InitProperty
        	set property InitProperty of CurrentStep to empty
        	add 1 to N
        end
        set property `aspect ratio` of CurrentStep to `WW:HH`
    end
    else if Action is `set content`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    end
    else if Action is `show`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    end
 	else if Action is `hide`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to object
            set property `blocks` of CurrentStep to Blocks
        end
    end
	else if Action is `pause`
    begin
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `hold`
    begin
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `fade up`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `fade down`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `crossfade`
    begin
    	put property `block` of CurrentStep into Block
        if Block is empty set property `block` of CurrentStep to `******`
    	put property `target` of CurrentStep into Target
        if Target is empty set property `target` of CurrentStep to `******`
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `transition`
    begin
    	put property `types` of CurrentStep into Types
        if Types is empty
        begin
        	set Types to array
            set property `types` of CurrentStep to Types
        end
    	put property `target` of CurrentStep into Target
        if Target is empty set property `target` of CurrentStep to `******`
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `goto`
    begin
    	put property `target` of CurrentStep into Target
        if Target is empty set property `target` of CurrentStep to `******`
    end
	else if Action is `load`
    begin
    	put property `url` of CurrentStep into URL
        if URL is empty set property `url` of CurrentStep to `******`
    end
    set element SelectedStep of Steps to CurrentStep
    set property `steps` of Presentation to Steps
	return

!	Save the current step
SaveCurrentStep:
	if SelectedStep is -1 return
	index TitleInput to SelectedStep
    put element SelectedStep of Steps into CurrentStep
    set property `title` of CurrentStep to the text of TitleInput
    index StepButton to SelectedStep
    set the text of StepButton to the content of TitleInput
	index LabelInput to SelectedStep
    set property `label` of CurrentStep to the content of LabelInput
	put property `action` of CurrentStep into Action
    if Action is `init`
    begin
        put 0 into N
        while N is less than the json count of InitProperties
        begin
            put element N of InitProperties into InitProperty
            index InitInput to N
            put InitInput into Item
            replace `"` with `&quot;` in Item
            set property InitProperty of CurrentStep to Item
            add 1 to N
        end
    end
    else if Action is `set content`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    end
    else if Action is `show`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    end
 	else if Action is `hide`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to object
            set property `blocks` of CurrentStep to Blocks
        end
    end
	else if Action is `pause`
    begin
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `hold`
    begin
    	index DurationInput to SelectedStep
    	set property `duration` of CurrentStep to the text of DurationInput
    end
	else if Action is `fade up`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `fade down`
    begin
    	put property `blocks` of CurrentStep into Blocks
        if Blocks is empty
        begin
        	set Blocks to array
            set property `blocks` of CurrentStep to Blocks
        end
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `crossfade`
    begin
    	put property `block` of CurrentStep into Block
        if Block is empty set property `block` of CurrentStep to `******`
    	put property `target` of CurrentStep into Target
        if Target is empty set property `target` of CurrentStep to `******`
    	put property `duration` of CurrentStep into Duration
        if Duration is empty set property `duration` of CurrentStep to 1
    end
	else if Action is `transition`
    begin
    	set property `block` of CurrentStep to BlockSelect
        set property `target` of CurrentStep to TargetSelect
    	set property `duration` of CurrentStep to the text of DurationInput
    end
	else if Action is `goto`
    begin
    	put property `target` of CurrentStep into Target
        if Target is empty set property `target` of CurrentStep to `******`
    end
	else if Action is `load`
    begin
    	put property `url` of CurrentStep into URL
        if URL is empty set property `url` of CurrentStep to `******`
    end
    set element SelectedStep of Steps to CurrentStep
    set property `steps` of Presentation to Steps

!	Tell the parent to refresh the script
    set Message to object
    set property `action` of Message to `refresh`
    set property `source` of Message to `steps`
    send Message to parent
	return