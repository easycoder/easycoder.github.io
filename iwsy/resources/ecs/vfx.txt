!	VFX

	script VFX
    
    import div Panel and variable Presentation
    
    div Editor
    div Title
    div Table
    div Row
    div Cell
    div Empty
    input ItemNameInput
    input AspectRatioInput
    input URLInput
    input DurationInput
    input EndSizeInput
    input EndXOffsetInput
    input EndYOffsetInput
    select VFXTypeSelect
    button EditButton
    button ShowButton
    button SaveButton
    img AddItem
    img Up
    img DeleteItem
    a Link
    variable Items
    variable Item
    variable Item2
    variable ItemName
    variable SelectedItem
    variable SavedIndex
    variable Message
    variable N
    variable NumItems
    variable VFXTypes
    variable Value
    
    put -1 into SelectedItem
    set VFXTypes to array
    json add `panzoom` to VFXTypes
    
    on message go to Start
    set ready
    stop
    
Start:
	if the message is `save`
    begin
        gosub to SaveSelectedItem
        stop
    end

Restart:
	put property `vfx` of Presentation into Items
    if Items is empty set Items to array

	clear Panel
    create Row in Panel
    set the style of Row to `display:flex`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center;margin-bottom:0.5em`
    set the content of Title to `VFX Items`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddItem in Link
    set the style of AddItem to `width:1em;margin-top:0.1em`
    set attribute `src` of AddItem to `/resources/icon/plus.png`
    on click AddItem
    begin
        set Item to object
        set property `name` of Item to `New vfx`
        json add Item to Items
        set property `vfx` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end

    put the json count of Items into NumItems
    if NumItems is greater than 0
    begin
        set the elements of EditButton to NumItems
        set the elements of Editor to NumItems
        set the elements of ShowButton to NumItems
        set the elements of SaveButton to NumItems
        set the elements of Up to NumItems
        set the elements of DeleteItem to NumItems
        set the elements of ItemNameInput to NumItems
        set the elements of VFXTypeSelect to NumItems
        set the elements of AspectRatioInput to NumItems
        set the elements of URLInput to NumItems
        set the elements of DurationInput to NumItems
        set the elements of EndSizeInput to NumItems
        set the elements of EndXOffsetInput to NumItems
        set the elements of EndYOffsetInput to NumItems
    end
    create Table in Panel
    set the style of Table to `width:100%`
    put 0 into N
    while N is less than NumItems
    begin
        index EditButton to N
        index Up to N
        index DeleteItem to N
        index Editor to N
        index ItemNameInput to N
        put element N of Items into Item
        put property `name` of Item into ItemName
        create Row in Table
        set the style of Row to `margin-bottom:0.2em`
        create Cell in Row
        set the style of Cell to `width:100%;display:flex`
        create EditButton in Cell
        set the style of EditButton to `flex:1`
        set the text of EditButton to ItemName
        if N is 0
        begin
            create Empty in Cell
            set the style of Empty to `width:1em;margin:0.2em 0 0 0.2em`
        end
        else
        begin
            create Link in Cell
            create Up in Link
            set the style of Up to `width:1em;margin:0.2em 0 0 0.2em`
            set attribute `src` of Up to `/resources/icon/up.png`
        end
        create Link in Cell
        create DeleteItem in Link
        set the style of DeleteItem to `width:1em;margin:0.2em 0 0 0.2em`
        set attribute `src` of DeleteItem to `/resources/icon/stop.png`
        create Editor in Row
        set the style of Editor to `margin:0.5em;border:1px solid black;padding:0.2em;display:none`
    	add 1 to N
    end
    on click EditButton
    begin
    	if the elements of EditButton is not greater than the index of EditButton stop
        put the index of EditButton into SavedIndex
        gosub to SaveSelectedItem
        put SavedIndex into SelectedItem
        put 0 into N
        while N is less than NumItems
        begin
        	if N is not SelectedItem
            begin
                index EditButton to N
                set style `background` of EditButton to `#eee`
                index Editor to N
                set style `display` of Editor to `none`
                clear Editor
            end
            add 1 to N
        end
        index EditButton to SelectedItem
        index ItemNameInput to SelectedItem
        index VFXTypeSelect to SelectedItem
        index AspectRatioInput to SelectedItem
        index URLInput to SelectedItem
        index DurationInput to SelectedItem
        index EndSizeInput to SelectedItem
        index EndXOffsetInput to SelectedItem
        index EndYOffsetInput to SelectedItem
        index ShowButton to SelectedItem
        index SaveButton to SelectedItem
        index Editor to SelectedItem
		if style `display` of Editor is `none`
        begin
            set style `display` of Editor to `block`
        	set style `background-color` of EditButton to `lightgray`
            put element SelectedItem of Items into Item
ReloadEditor:
			clear Editor
            create Row in Editor
            set the style of Row to `display:flex`
            create Cell in Row
            set the style of Cell to `width:5em`
            set the content of Cell to `Type`
            create VFXTypeSelect in Row
            set the style of VFXTypeSelect to `flex:1`
            set VFXTypeSelect from VFXTypes as property `type` of Item
            on change VFXTypeSelect
            begin
            	gosub to SaveSelectedItem
            	set property `type` of Item to VFXTypeSelect
                set element SelectedItem of Items to Item
            	go to ReloadEditor
            end
            create Row in Editor
            set the style of Row to `display:flex`
            create Cell in Row
            set the style of Cell to `width:5em`
            set the content of Cell to `Name`
            create ItemNameInput in Row
            set the style of ItemNameInput to `flex:1`
            set the text of ItemNameInput to property `name` of Item
            if VFXTypeSelect is `panzoom`
            begin
            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `Aspect`
                create AspectRatioInput in Row
                set the style of AspectRatioInput to `flex:1`
                put property `aspect` of Item into Value
                if Value is empty put `4:3` into Value
                set the text of AspectRatioInput to Value
                
            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `Image`
                create URLInput in Row
                set the style of URLInput to `flex:1`
                put property `url` of Item into Value
                if Value is empty put empty into Value
                set the text of URLInput to Value

            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `Duration`
                create DurationInput in Row
                set the style of DurationInput to `flex:1`
                put property `duration` of Item into Value
!                if Value is empty put 1 into Value
                set the text of DurationInput to Value
                
            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `End Size`
                create EndSizeInput in Row
                set the style of EndSizeInput to `flex:1`
                put property `endsize` of Item into Value
                set the text of EndSizeInput to Value
                
            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `End Xoff`
                create EndXOffsetInput in Row
                set the style of EndXOffsetInput to `flex:1`
                put property `endxoff` of Item into Value
                set the text of EndXOffsetInput to Value
                
            	create Row in Editor
                set the style of Row to `display:flex`
                create Cell in Row
                set the style of Cell to `width:5em`
                set the content of Cell to `End Yoff`
                create EndYOffsetInput in Row
                set the style of EndYOffsetInput to `flex:1`
                put property `endyoff` of Item into Value
                set the text of EndYOffsetInput to Value
            end
            create Row in Editor
            set the style of Row to `display:flex;margin-top:0.5em`
            create SaveButton in Row
            set the style of SaveButton to `flex:1;margin-right:0.2em`
            set the text of SaveButton to `Save`
            on click SaveButton gosub to SaveSelectedItem
            create ShowButton in Row
            set the style of ShowButton to `flex:1;margin-left:0.2em`
            set the text of ShowButton to `Show`
            on click ShowButton
            begin
            	gosub to SaveSelectedItem
            	put element SelectedItem of Items into Item
                set Message to object
                set property `action` of Message to `panzoom`
                set property `item` of Message to Item
                send Message to parent
            end
        end
        else
        begin
            clear Editor
            set style `display` of Editor to `none`
        end
    end
    on click Up
    begin
    	put the index of Up into N
        put element N of Items into Item
        take 1 from N
        put element N of Items into Item2
        set element N of Items to Item
        add 1 to N
        set element N of Items to Item2
        set property `vfx` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end
    on click DeleteItem
    begin
    	put the index of DeleteItem into N
        put property `vfx` of Presentation into Items
        json delete element N of Items
        set property `vfx` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end
	stop

!	Save the selected content
SaveSelectedItem:
	if SelectedItem is -1 return
    if the json count of Items is 0 return
    index ItemNameInput to SelectedItem
    index VFXTypeSelect to SelectedItem
    index AspectRatioInput to SelectedItem
    index EditButton to SelectedItem
    put element SelectedItem of Items into Item
    put ItemNameInput into Value
    replace ` ` with `-` in Value
    set the text of ItemNameInput to Value
    set the text of EditButton to Value
    set property `name` of Item to Value
    if property `type` of Item is `panzoom`
    begin
    	index AspectRatioInput to SelectedItem
    	index URLInput to SelectedItem
    	index DurationInput to SelectedItem
    	index EndSizeInput to SelectedItem
    	index EndXOffsetInput to SelectedItem
    	index EndYOffsetInput to SelectedItem
    	set property `aspect` of Item to AspectRatioInput
    	set property `url` of Item to URLInput
    	set property `duration` of Item to DurationInput
    	set property `endsize` of Item to EndSizeInput
    	set property `endxoff` of Item to EndXOffsetInput
    	set property `endyoff` of Item to EndYOffsetInput
    end
    set element SelectedItem of Items to Item
    set property `vfx` of Presentation to Items

!	Tell the parent to refresh the script
    set Message to object
    set property `action` of Message to `refresh`
    set property `source` of Message to `vfx`
    send Message to parent
	return