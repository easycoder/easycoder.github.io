! Script editor

  script ScriptEditor

  div Body
  div Container
  div Buttons
  div ScriptName
  div ContentDiv
  input NameEditor
  textarea ContentEditor
  span Status
  span Span
  button Open
  button Save
  button Delete
  variable Name
  variable Content
  variable Current
  variable Password
  variable Valid

! The browser
  div Overlay
  div Scroller
  div Media
  div FileListing
  div FileRow
  button CloseButton
  a FileName
  variable Alpha
  variable FileList
  variable FileCount
  variable File
  variable Files
  variable N
  variable FileIsOpen
  variable Item

  set the title to `Script Editor`

  get Password from storage as `.password`
  if Password is empty
    put prompt `Please type the admin password` with `` into Password
  rest get Valid from `verify/` cat Password
  or begin
  print Valid
    alert `Unable to verify password.`
    put empty into storage as `.password`
  end
  if Valid is `yes` put Password into storage as `.password`
  
  create Body
  create Container in Body
  set the style of Container to `width:70%;margin:0 auto;background #ffe`

  create Buttons in Container
  set the style of Buttons to `text-align:center`

  create Open in Buttons
  set the style of Open to `margin-right:0.5em`
  set the text of Open to `Open`
  create Save in Buttons
  set the text of Save to `Save`
  set the style of Save to `margin-right:4em`
  create Delete in Buttons
  set the text of Delete to `Delete`
  create Status in Buttons
  set the style of Status to `position:absolute;float:right;padding-left:2em;color:green`

  create ScriptName in Container
  set the style of ScriptName to `display:flex;margin:0.5em 0`
  create Span in ScriptName
  set the style of Span to `flex:15`
  set the content of Span to `Script name:&nbsp;`
  create NameEditor in ScriptName
  set the style of NameEditor to `flex:85;display:inline-block`
  set the size of NameEditor to 40

  create ContentDiv in Container
  set the style of ContentDiv to `width:100%;height:85%;border:1px solid lightgray`

  create ContentEditor in ContentDiv
  set the style of ContentEditor to `width:100%;height:100%`

  codemirror init basic profile `https://cdn.jsdelivr.net/gh/easycoder/easycoder.github.io/dist/plugins/codemirror-ecs.js`
  require css `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.css`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/search.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/searchcursor.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/jump-to-line.js`

  create Overlay in Body
  set the style of Overlay to
    `position:absolute;top:0;left:0;width:100vw;height:100vh;background:rgba(0,0,0,0.0);display:none`

  create Media in Overlay
  set style of Media to `display:none;width:100%;height:100%;text-align:center`

  create FileListing in Media
  set the style of FileListing to
    `display:none;width:50%;height:75%;margin:auto;background-color:white;`
    cat `padding:2em 2em 3em 2em;text-align:center;position: absolute;top: 50%;left: 50%;`
    cat `transform: translateX(-50%) translateY(-50%)`

  create Scroller in FileListing
  set the style of Scroller to `height:100%;overflow:scroll;text-align:left`

  create CloseButton in FileListing
  set the text of CloseButton to `Close`
  
  codemirror attach to ContentEditor
  codemirror set content of ContentEditor to Content
  set FileIsOpen

  put empty into Current

  on click Save
  begin
    put the content of NameEditor into Name
    if Name is empty
    begin
      set the content of Status to `No script name has been given`
      go to ResetStatus
    end
    replace ` ` with `_` in Name
    if the position of `.txt` in Name is -1 put Name cat `.txt` into Name
    codemirror close ContentEditor
    put the content of ContentEditor into Content
    if Content is not Current
    begin
      rest post Content to `save/` cat Password cat `/ecs/` cat Name
      put Content into Current
      set the content of Status to `Script '` cat Name cat `' saved`
      fork to ResetStatus
    end
    else
    begin
      set the content of Status to `Nothing has changed`
      fork to ResetStatus
    end
    codemirror attach to ContentEditor
  end

  on click Open go to DoOpen

  on click Delete
  begin
    put the content of NameEditor into Name
    if Name is empty
    begin
      alert `Nothing to delete.`
      stop
    end
    if confirm `Are you sure you want to delete '` cat Name cat `'?`
    begin
      codemirror close ContentEditor
      rest post to `delete/` cat Password cat `/ecs/` cat Name
      set the content of Status to `Script '` cat Name cat `' deleted`
      set the content of NameEditor to empty
      put empty into Content
      set the content of ContentEditor to Content
      put Content into Current
      go to ResetStatus
    end
  end
  stop

DoOpen:
  if FileIsOpen codemirror close ContentEditor
  put the content of ContentEditor into Content
  if Content is not Current
  begin
    if confirm `Content has changed. Do you want to save it?`
    begin
      rest post Content to `save/` cat Password cat `/ecs/' cat CurrentName
	  codemirror attach to ContentEditor
	  codemirror set content of ContentEditor to Content
    end
    else
    begin
      codemirror attach to ContentEditor
      codemirror set content of ContentEditor to Content
      stop
    end
  end

! Animate the background
  set style `display` of Overlay to `block`
  put 0 into Alpha
  while Alpha is less than 8
  begin
    set style `background-color` of Overlay to `rgba(0,0,0,0.` cat Alpha cat `)`
    wait 4 ticks
    add 1 to Alpha
  end
  wait 10 ticks

! Make the browser panel visible
  set style `display` of Media to `block`
  set style `display` of FileListing to `inline-block`

! Fill the browser with content from the server
  rest get Files from `list/` cat Password cat `/ecs?v=` cat now
  put the json count of Files into FileCount
  put empty into Content
  put 0 into N
  while N is less than FileCount
  begin
  	put element N of Files into Item
    if property `type` of Item is `txt` json add Item to Content
    add 1 to N
  end
  json sort Content
  put empty into FileList
  put the json count of Content into FileCount
  set the elements of File to FileCount
  set the elements of FileName to FileCount
! Add a row for each file
  put empty into FileList
  put 0 into N
  while N is less than FileCount
  begin
    index File to N
    index FileName to N
    put `<div id="ec-file-row-INDEX" style="clear:both;padding:0.25em 0;">`
      cat `<a id="ec-file-name-INDEX" href="#"></a></div>` into File
    replace `INDEX` with N in File
    if N is even replace `ODDEVEN` with `ec-even` in File
    else replace `ODDEVEN` with `ec-odd` in File
    put FileList cat File into FileList
    add 1 to N
  end

  set the content of Scroller to FileList
! Add the document names
  put 0 into N
  while N is less than FileCount
  begin
    index File to N
    index FileName to N
    put element N of Content into File
    attach FileRow to `ec-file-row-` cat N
    attach FileName to `ec-file-name-` cat N
    put property `name` of File into File
    set the content of FileName to File
    if N is even set style `background` of FileRow to `lightgray`
    on click FileName go to SelectFile
    add 1 to N
  end
  on click CloseButton
  begin
    put Current into Content
    go to CloseBrowser
  end
  stop

SelectFile:
  index File to the index of FileName
  set the content of NameEditor to File
  rest get Content from `/resources/ecs/` cat File cat `?v=` cat now
  put Content into Current
  set the content of Status to `Script '` cat File cat `' loaded`
  fork to ResetStatus
  set the title to `Script Editor - ` cat File

CloseBrowser:
  set style `background-color` of Overlay to `rgba(0,0,0,0.0)`
  set style `display` of Overlay to `none`
  set style `display` of Media to `none`
  codemirror attach to ContentEditor
  codemirror set content of ContentEditor to Content
  stop

ResetStatus:
  wait 2
  set the content of Status to ``
  stop