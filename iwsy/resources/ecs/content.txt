!	Content

	script Content
    
    import div Panel and variable Presentation
    
    div Editor
    div Title
    div Table
    div Row
    div Cell
    div Empty
    input ItemNameInput
    textarea TextArea
    button EditButton
    button SaveButton
    img AddItem
    img Up
    img DeleteItem
    a Link
    variable Items
    variable Item
    variable Item2
    variable ItemNames
    variable ItemName
    variable SelectedItem
    variable SavedIndex
    variable Message
    variable N
    variable NumItems
    variable Content
    
    put -1 into SelectedItem
    
    on message go to Start
    set ready
    stop
    
Start:
	if the message is `save`
    begin
        gosub to SaveSelectedItem
        stop
    end

Restart:
	put property `content` of Presentation into Items
    set ItemNames to array
    put 0 into N
    while N is less than the json count of Items
    begin
    	put element N of Items into Item
        json add property `name` of Item to ItemNames
    	add 1 to N
    end

	clear Panel
    create Row in Panel
    set the style of Row to `display:flex`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center;margin-bottom:0.5em`
    set the content of Title to `Content Items`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddItem in Link
    set the style of AddItem to `width:1em;margin-top:0.1em`
    set attribute `src` of AddItem to `/resources/icon/plus.png`
    on click AddItem
    begin
        set Item to object
        set property `name` of Item to `New content`
        set property `content` of Item to empty
        json add Item to Items
        set property `content` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end

    put the json count of Items into NumItems
    if NumItems is greater than 0
    begin
        set the elements of EditButton to NumItems
        set the elements of Editor to NumItems
        set the elements of SaveButton to NumItems
        set the elements of Up to NumItems
        set the elements of DeleteItem to NumItems
        set the elements of ItemNameInput to NumItems
        set the elements of TextArea to NumItems
    end
    create Table in Panel
    set the style of Table to `width:100%`
    put 0 into N
    while N is less than NumItems
    begin
        index EditButton to N
        index Up to N
        index DeleteItem to N
        index Editor to N
        index ItemNameInput to N
        put element N of Items into Item
        put property `name` of Item into ItemName
        create Row in Table
        set the style of Row to `margin-bottom:0.2em`
        create Cell in Row
        set the style of Cell to `width:100%;display:flex`
        create EditButton in Cell
        set the style of EditButton to `flex:1`
        set the text of EditButton to ItemName
        if N is 0
        begin
            create Empty in Cell
            set the style of Empty to `width:1em;margin:0.2em 0 0 0.2em`
        end
        else
        begin
            create Link in Cell
            create Up in Link
            set the style of Up to `width:1em;margin:0.2em 0 0 0.2em`
            set attribute `src` of Up to `/resources/icon/up.png`
        end
        create Link in Cell
        create DeleteItem in Link
        set the style of DeleteItem to `width:1em;margin:0.2em 0 0 0.2em`
        set attribute `src` of DeleteItem to `/resources/icon/stop.png`
        create Editor in Row
        set the style of Editor to `margin:0.5em;border:1px solid black;padding:0.2em;display:none`
    	add 1 to N
    end
    on click EditButton
    begin
    	if the elements of EditButton is not greater than the index of EditButton stop
        put the index of EditButton into SavedIndex
        gosub to SaveSelectedItem
        put SavedIndex into SelectedItem
        put 0 into N
        while N is less than NumItems
        begin
        	if N is not SelectedItem
            begin
                index EditButton to N
                set style `background` of EditButton to `#eee`
                index Editor to N
                set style `display` of Editor to `none`
                clear Editor
            end
            add 1 to N
        end
        index EditButton to SelectedItem
        index ItemNameInput to SelectedItem
        index TextArea to SelectedItem
        index SaveButton to SelectedItem
        index Editor to SelectedItem
		if style `display` of Editor is `none`
        begin
            set style `display` of Editor to `block`
            put element SelectedItem of Items into Item
        	set style `background-color` of EditButton to `lightgray`
            create Row in Editor
            set the style of Row to `display:flex`
            create Cell in Row
            set the style of Cell to `width:5em`
            set the content of Cell to `Name`
            create ItemNameInput in Row
            set the style of ItemNameInput to `flex:1`
            set the text of ItemNameInput to property `name` of Item
            create TextArea in Editor
            set the style of TextArea to `width:100%;max-width:100%;height:20em`
            put property `content` of Item into Content
            replace `%0a` with newline in Content
            set the content of TextArea to Content
            create SaveButton in Editor
            set the style of SaveButton to `width:100%`
            set the text of SaveButton to `Save`
            on click SaveButton gosub to SaveSelectedItem
        end
        else
        begin
            clear Editor
            set style `display` of Editor to `none`
        end
    end
    on click Up
    begin
    	put the index of Up into N
        put element N of Items into Item
        take 1 from N
        put element N of Items into Item2
        set element N of Items to Item
        add 1 to N
        set element N of Items to Item2
        set property `content` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end
    on click DeleteItem
    begin
    	put the index of DeleteItem into N
        put property `content` of Presentation into Items
        json delete element N of Items
        set property `content` of Presentation to Items
        put -1 into SelectedItem
        go to Restart
    end
	stop

!	Save the selected content
SaveSelectedItem:
	if SelectedItem is -1 return
    index ItemNameInput to SelectedItem
    index EditButton to SelectedItem
    put element SelectedItem of Items into Item
    set property `name` of Item to the text of ItemNameInput
    set the text of EditButton to the text of ItemNameInput
    index TextArea to SelectedItem
    put the content of TextArea into Content
    replace newline with `%0a` in Content
    replace `"` with `&quot;` in Content
    set property `content` of Item to Content
    set element SelectedItem of Items to Item
    set property `content` of Presentation to Items

!	Tell the parent to refresh the script
    set Message to object
    set property `action` of Message to `refresh`
    set property `source` of Message to `content`
    send Message to parent
	return