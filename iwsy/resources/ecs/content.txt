!	Content

	script Content
    
    import div Panel and variable Presentation
    
    div Editor
    div Row
    div Cell
    div Title
    table Table
    tr TR
    td TD
    span ItemNameSpan
    input ItemNameInput
    textarea TextArea
    button EditButton
    img AddItem
    img DeleteItem
    a Link
    variable Items
    variable Item
    variable ItemNames
    variable ItemName
    variable SelectedItem
    variable Action
    variable Message
    variable N
    variable B
    
    put -1 into SelectedItem
    
    on message go to Start
    set ready
    stop
    
Start:
	if the message is `save`
    begin
        gosub to SaveSelectedItem
        stop
    end

Restart:
	put property `content` of Presentation into Items
    put the json keys of Items into ItemNames

	clear Panel
    create Row in Panel
    set the style of Row to `display:flex`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center;margin-bottom:0.5em`
    set the content of Title to `Content Items`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddItem in Link
    set the style of AddItem to `width:1em;margin-top:0.1em`
    set attribute `src` of AddItem to `resources/icon/plus.png`
    on click AddItem
    begin
    	put prompt `Name of new content item:` with `new content` into ItemName
        set Item to object
        set property ItemName of Items to Item
        set property `content` of Presentation to Items
        go to Restart
    end

	put the json count of ItemNames into N
    set the elements of EditButton to N
    set the elements of Editor to N
    set the elements of DeleteItem to N
    set the elements of TextArea to N
    create Table in Panel
    set the style of Table to `width:100%`
    put 0 into B
    while B is less than the elements of EditButton
    begin
    	put element B of ItemNames into ItemName
        create TR in Table
        create TD in TR
        set the style of TD to `width:8em;border:1px solid black`
        index ItemNameSpan to B
        create ItemNameSpan in TD
        set the content of ItemNameSpan to ItemName
        index ItemNameInput to B
        create ItemNameInput in TD
        set the style of ItemNameInput to `width:100%;display:none`
        set the content of ItemNameInput to ItemName
        create TD in TR
    	set the style of TD to `border:1px solid black`
        index EditButton to B
        index DeleteItem to B
        index Editor to B
        create Row in TD
        set the style of Row to `display:flex`
        create EditButton in Row
        set the style of EditButton to `flex:1`
        set the text of EditButton to `Edit`
        create Cell in Row
        set the style of Cell to `width:1.4em;text-align:center`
        create Link in Cell
        create DeleteItem in Link
        set the style of DeleteItem to `width:1em;margin-top:0.1em`
        set attribute `src` of DeleteItem to `/resources/icon/stop.png`
        create Editor in TD
        set the style of Editor to `display:none`
    	add 1 to B
    end
    on click EditButton
    begin
    	gosub to SaveSelectedItem
        put the index of EditButton into SelectedItem
        put the text of EditButton into Action
        put 0 into N
        while N is less than the json count of ItemNames
        begin
           	index EditButton to N
        	set style `background` of EditButton to ``
            set the text of EditButton to `Edit`
            index ItemNameSpan to N
            index ItemNameInput to N
            set the content of ItemNameSpan to the text of ItemNameInput
            set style `display` of ItemNameSpan to `inline`
            set style `display` of ItemNameInput to `none`
            index Editor to N
            set style `display` of Editor to `none`
            add 1 to N
        end
        index EditButton to SelectedItem
    	if Action is `Edit`
        begin
        	set style `background` of EditButton to `lightgray`
        	set the text of EditButton to `Close`
            index ItemNameSpan to SelectedItem
            index ItemNameInput to SelectedItem
            index TextArea to SelectedItem
            set style `display` of ItemNameSpan to `none`
            set style `display` of ItemNameInput to `inline`
            index Editor to SelectedItem
            set style `display` of Editor to `block`
            clear Editor
            put property ItemName of Items into Item
            create TextArea in Editor
            set the style of TextArea to `width:100%;max-width:100%;height:8em`
            put element SelectedItem of ItemNames into ItemName
            replace `%0a` with newline in Item
            set the content of TextArea to Item
        end
        else gosub to SaveSelectedItem
    end
    on click DeleteItem
    begin
    	put the index of DeleteItem into N
        put element N of ItemNames into ItemName
        put property `content` of Presentation into Items
        json delete property ItemName of Items
        set property `content` of Presentation to Items
        go to Restart
    end
	stop

!	Save the seleced content
SaveSelectedItem:
	if SelectedItem is -1 return
    put the content of TextArea into Item
    replace newline with `%0a` in Item
    set property ItemName of Items to Item
    index ItemNameInput to SelectedItem
    if ItemNameInput is not ItemName
    begin
    	json delete property ItemName of Items
        put ItemNameInput into ItemName
        set element SelectedItem of ItemNames to ItemName
    	set property ItemName of Items to Item
    end
    set property `content` of Presentation to Items

!	Tell the parent to refresh the script
    set Message to object
    set property `action` of Message to `refresh`
    set property `source` of Message to `content`
    send Message to parent
	return