!	Content

	script Content
    
    import div Panel and variable Presentation
    
    div Editor
    div Row
    div Cell
    div Title
    table Table
    tr TR
    td TD
    span ItemNameSpan
    span Span
    input ItemNameInput
    select TypeSelect
    textarea TextArea
    button EditButton
    img AddItem
    img DeleteItem
    a Link
    variable Items
    variable Item
    variable ItemNames
    variable ItemName
    variable TypeNames
    variable Content
    variable SelectedItem
    variable Action
    variable N
    variable B
    
    put -1 into SelectedItem
    
    set TypeNames to array
    json add `text` to TypeNames
    json add `image` to TypeNames
    
    on message go to Start
    set ready
    stop
    
Start:
	if the message is `save`
    begin
    	if SelectedItem is -1 stop
        go to SaveSelectedItem
    end

Restart:
	put property `content` of Presentation into Items
    put the json keys of Items into ItemNames

	clear Panel
    create Row in Panel
    set the style of Row to `display:flex;margin-top:0.5em`
    create Title in Row
    set the style of Title to
    	`flex:1;font-size:110%;font-weight:bold;background:lightgray;text-align:center`
    set the content of Title to `Content Items`
    create Cell in Row
    set the style of Cell to `width:1.4em;text-align:center`
    create Link in Cell
    create AddItem in Link
    set the style of AddItem to `width:1em;margin-top:0.1em`
    set attribute `src` of AddItem to `resources/icon/plus.png`
    on click AddItem
    begin
    	put prompt `Name of new content item:` with `new content` into ItemName
        set Item to object
        set property ItemName of Items to Item
        set property `content` of Presentation to Items
        go to Restart
    end

	put the json count of ItemNames into N
    set the elements of EditButton to N
    set the elements of Editor to N
    set the elements of DeleteItem to N
    set the elements of TypeSelect to N
    set the elements of TextArea to N
    create Table in Panel
    if N is greater than 0 set the style of Table to `width:100%;border:1px solid black`
    put 0 into B
    while B is less than the elements of EditButton
    begin
    	put element B of ItemNames into ItemName
        create TR in Table
        create TD in TR
        set the style of TD to `width:8em;border:1px solid black`
        create ItemNameSpan in TD
        set the content of ItemNameSpan to ItemName
        create ItemNameInput in TD
        set the style of ItemNameInput to `width:100%;display:none`
        set the content of ItemNameInput to ItemName
        create TD in TR
    	set the style of TD to `border:1px solid black`
        index EditButton to B
        index DeleteItem to B
        index Editor to B
        create Row in TD
        set the style of Row to `display:flex`
        create EditButton in Row
        set the style of EditButton to `flex:1`
        set the text of EditButton to `Edit`
        create Cell in Row
        set the style of Cell to `width:1.4em;text-align:center`
        create Link in Cell
        create DeleteItem in Link
        set the style of DeleteItem to `width:1em;margin-top:0.1em`
        set attribute `src` of DeleteItem to `/resources/icon/stop.png`
        create Editor in TD
        set the style of Editor to `display:none`
    	add 1 to B
    end
    on click EditButton
    begin
        put the index of EditButton into SelectedItem
        put the text of EditButton into Action
        put 0 into N
        while N is less than the json count of ItemNames
        begin
           	index EditButton to N
        	set style `background` of EditButton to ``
            set the text of EditButton to `Edit`
            index ItemNameSpan to N
            index ItemNameInput to N
            set the content of ItemNameSpan to the text of ItemNameInput
            set style `display` of ItemNameSpan to `inline`
            set style `display` of ItemNameInput to `none`
            index Editor to N
            set style `display` of Editor to `none`
            add 1 to N
        end
        index EditButton to SelectedItem
    	if Action is `Edit`
        begin
        	set style `background` of EditButton to `lightgray`
        	set the text of EditButton to `Content`
            index ItemNameSpan to SelectedItem
            index ItemNameInput to SelectedItem
            index TypeSelect to SelectedItem
            index TextArea to SelectedItem
            set style `display` of ItemNameSpan to `none`
            set style `display` of ItemNameInput to `inline`
            index Editor to SelectedItem
            set style `display` of Editor to `block`
            clear Editor
            put property ItemName of Items into Item
            create Row in Editor
            set the style of Row to `width:100%;display:flex`
            create Span in Row
            set the style of Span to `flex:1`
            set the content of Span to `Type:`
            create TypeSelect in Row
            set the style of TypeSelect to `flex:1`
            set TypeSelect from TypeNames as property `type` of Item
            create TextArea in Editor
            set the style of TextArea to `width:100%;max-width:100%;height:8em`
            put element SelectedItem of ItemNames into ItemName
            put property `content` of Item into Content
            replace `%0a` with newline in Content
            set the content of TextArea to Content
            put property ItemName of Items into Item
        end
        else go to SaveSelectedItem
    end
    on click DeleteItem
    begin
    	put the index of DeleteItem into N
        put element N of ItemNames into ItemName
        put property `content` of Presentation into Items
        json delete property ItemName of Items
        set property `content` of Presentation to Items
        go to Restart
    end
	stop

!	Save the seleced content
SaveSelectedItem:
	set property `type` of Item to TypeSelect
    put the content of TextArea into Content
    replace newline with `%0a` in Content
    set property `content` of Item to Content
    set property ItemName of Items to Item
    index ItemNameInput to SelectedItem
    if ItemNameInput is not ItemName
    begin
    	json delete property ItemName of Items
        put ItemNameInput into ItemName
        set element SelectedItem of ItemNames to ItemName
    	set property ItemName of Items to Item
    end
    set property `content` of Presentation to Items
	stop