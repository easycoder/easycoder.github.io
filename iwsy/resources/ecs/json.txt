!	JSON

	script JSON
    
    import div Container and variable Presentation
    
    div Panel
    div Row
    select SectionSelector
    textarea Script
    button SaveButton
    variable Sections
    variable Section
    variable Content
    variable LastSaved
    
    set Sections to array
    json add `All` to Sections
    json add `Steps` to Sections
    json add `Blocks` to Sections
    json add `Content` to Sections
    json add `VFX` to Sections
    
    on message go to Start
    set ready
    stop
    
Start:
	if the message is `save`
    begin
        stop
    end
    
    clear Container
    create Panel in Container
    set the style of Panel to `height:100%;display:flex;flex-direction:column`
    
    create Row in Panel
    set the style of Row to `font-size:1.2em;font-weight:bold;margin-bottom:0.2em`
    set the content of Row to `Section: `
    
    create SectionSelector in Row
    set the style of SectionSelector to `height:1.5em`
    set SectionSelector from Sections as `All`
    on change SectionSelector
    begin
    	if Script is not LastSaved
        begin
            if confirm `Content has changed. Do you want to save it?`
            begin
                if Section set property Section of Presentation to Script
            end
        end
    	if SectionSelector is `All`
        begin
        	put empty into Section
    		put Presentation into Content
            go to SS2
        end
        else if SectionSelector is `Steps`
        begin
        	put `steps` into Section
            go to SelectSection
        end
        else if SectionSelector is `Blocks`
        begin
        	put `blocks` into Section
            go to SelectSection
        end
        else if SectionSelector is `Content`
        begin
        	put `content` into Section
            go to SelectSection
        end
        else if SectionSelector is `VFX`
        begin
        	put `vfx` into Section
            go to SelectSection
        end
    end

	create Script in Panel
    set the style of Script to `flex:1;width:100%`
    
    set the content of Script to Presentation
    put Presentation into LastSaved
    
    create SaveButton in Panel
    set the style of SaveButton to `display:block;width:100%`
    set the text of SaveButton to `Save`
    on click SaveButton
    begin
    	if Section set property Section of Presentation to Script
        else put Script into Presentation
    	put Script into LastSaved
        json format Presentation
    end
    
    put empty into Section
    put Presentation into Content
    go to SS2

SelectSection:
	put property Section of Presentation into Content
SS2:
    json format Content
    put Content into LastSaved
    set the content of Script to Content
	stop