! Script Editor

  script ScriptEditor

  div Body
  div Container
  div Buttons
  div ContentDiv
  textarea ContentEditor
  span Status
  button Save
  variable File
  variable Content
  variable Current

  set the title to `Script Editor`

  create Body
  create Container in Body
  set the style of Container to `width:70%;margin:0 auto;background #ffe`

  create Buttons in Container
  set the style of Buttons to `text-align:center`

  create Save in Buttons
  set the text of Save to `Save`
  create Status in Buttons
  set the style of Status to `position:absolute;float:right;padding-left:2em;color:green`

  create ContentDiv in Container
  set the style of ContentDiv to `width:100%;height:95%;border:1px solid lightgray`

  create ContentEditor in ContentDiv
  set the style of ContentEditor to `width:100%;height:100%`

  codemirror init basic profile `/codemirror-ecs.js`
  require css `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.css`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/search.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/searchcursor.js`
  require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/jump-to-line.js`

  put empty into Current

  on click Save
  begin
    codemirror close ContentEditor
    put the content of ContentEditor into Content
    if Content is not Current
    begin
      rest post Content to `save/storyteller.txt`
      put Content into Current
      set the content of Status to `Script saved`
      fork to ResetStatus
    end
    else
    begin
      set the content of Status to `Nothing has changed`
      fork to ResetStatus
    end
    codemirror attach to ContentEditor
  end

SelectFile:
  put `storyteller.txt` into File
  rest get Content from `/` cat File
  put Content into Current
  set the content of Status to `Script '` cat File cat `' loaded`
  fork to ResetStatus
  set the title to File

  codemirror attach to ContentEditor
  codemirror set content of ContentEditor to Content
  stop

ResetStatus:
  wait 2
  set the content of Status to ``
  stop
