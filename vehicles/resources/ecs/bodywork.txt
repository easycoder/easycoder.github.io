!   Bodywork
    
    import variable Vehicle and div Part

    script Bodywork
    
    div Container
    div Marker
    span Circle
    table DamageTable
    table Table
    tr TR
    td TD
    img Image
    img Plus
    img Finger
    img Up
    img Delete
    input Input
    input Description
    variable Damage
    variable Item
    variable Severity
    variable CurrentSeverity
    variable Left
    variable Top
    variable X
    variable Y
    variable Width
    variable W
    variable N
    variable M
    variable Temp
    variable GotMarker
    variable Color
    variable YELLOW
    variable ORANGE
    variable RED
    
    put `230,230,0` into YELLOW
    put `204,153,0` into ORANGE
    put `255,0,102` into RED
    
    on message
    begin
    	if the message is `show`
        begin
        	gosub to ShowDamage
            stop
        end
        else go to DoBodywork
    end
    set ready
    stop

DoBodywork:
	if not property `damage` of Vehicle
    begin
    	set Damage to array
        set property `damage` of Vehicle to Damage
    end
    put property `damage` of Vehicle into Damage
    
    put YELLOW into Color
    put 0 into CurrentSeverity
        
	index Part to 2
	create Container in Part
    set the style of Container to `position:relative`
    create Image in Container
    set the style of Image to `width:100%`
    set attribute `src` of Image to `resources/img/vehicle.jpg`

    create Table in Part
    set the style of Table to `width:100%`
    
    create TR in Table
    set style `height` of TR to `3em`
    create TD in TR
    create Input in TD
    set the style of Input to `width:100%`
    create TD in TR
    set the style of TD to `width:8em;text-align:right`
    create Circle in TD
    set the style of Circle to
    	`display:inline-block;width:2em;height:2em;margin-right:0.5em;border-radius:1em;`
        cat `border:1px solid grey;background:rgb(` cat Color cat `)`
    create Finger in TD
    set the style of Finger to `width:2em;height:2em;margin-right:0.5em`
    set attribute `src` of Finger to `resources/icons/finger.png`
    create Plus in TD
    set the style of Plus to `width:2em;height:2em;margin-right:0.5em`
    set attribute `src` of Plus to `resources/icons/plus.png`
    
    create DamageTable in Part
    set the style of DamageTable to `width:100%;margin-bottom:0.5em`
    
    on click Circle go to OnClickCircle    
    on click Finger go to OnClickFinger
    on click Plus go to OnClickPlus
    
    clear GotMarker
    
	stop

OnClickCircle:
    add 1 to CurrentSeverity
    if CurrentSeverity is 3 put 0 into CurrentSeverity
    put CurrentSeverity into Severity
    gosub to SetSeverityColor
    set style `background` of Circle to `rgb(` cat Color cat `)`
    set property `severity` of Item to CurrentSeverity
	stop

OnClickFinger:
    set attribute `src` of Finger to `resources/icons/cross.png`
    gosub to ClearMarkers
    on click Finger
    begin
    	gosub to RestoreFinger
        gosub to ShowDamage
    end
    put CurrentSeverity into Severity
    gosub to SetSeverityColor
    on click Image go to OnClickImage
	stop

RestoreFinger:
    on click Image stop
    clear GotMarker
    set attribute `src` of Finger to `resources/icons/finger.png`
    on click Finger go to OnClickFinger
	return

OnClickImage:
	put click left into Left
    put click top into Top
    set GotMarker
    gosub to DrawMarker
    multiply Left by 1000
    divide Left by the width of Image
    multiply Top by 1000
    divide Top by the height of Image
    on click Image stop
	stop

OnClickPlus:
	if not GotMarker stop
    gosub to RestoreFinger
	set Item to object
    set property `description` of Item to the text of Input
    set the text of Input to empty
    set property `severity` of Item to CurrentSeverity
    set property `left` of Item to Left
    set property `top` of Item to Top
    append Item to Damage
    set property `damage` of Vehicle to Damage
    gosub to ShowDamage
    clear GotMarker
	stop

ShowDamage:
	gosub to ClearMarkers
    clear DamageTable
    set the elements of Marker to the json count of Damage
    set the elements of Up to the json count of Damage
    set the elements of Delete to the json count of Damage
	put 0 into N
    while N is less than the json count of Damage
    begin
    	put element N of Damage into Item
        put property `severity` of Item into Severity
        gosub to SetSeverityColor
        put property `left` of Item into Left
        put property `top` of Item into Top
        multiply Left by the width of Image
        divide Left by 1000
        multiply Top by the height of Image
        divide Top by 1000
        index Marker to N
        gosub to DrawMarker
        
        create TR in DamageTable
    	set style `height` of TR to `2.2em`
        create TD in TR
        set the style of TD to `text-align:center`
        add 1 to N giving M
        set the content of TD to M
        create TD in TR
        create Description in TD
        set the style of Description to `width:100%`
        set the text of Description to property `description` of Item
        create TD in TR
        set the style of TD to `width:5.5em;text-align:right`
        if N is not 0
        begin
	        index Up to N
	        create Up in TD
	        set the style of Up to `width:2em;height:2em;margin-right:0.5em`
	        set attribute `src` of Up to `resources/icons/up.png`
        end
        index Delete to N
        create Delete in TD
        set the style of Delete to `width:2em;height:2em;margin-right:0.5em`
        set attribute `src` of Delete to `resources/icons/cross.png`
    	add 1 to N
    end
    on click Up go to MoveUp
    on click Delete
    begin
    	put the index of Delete into N
      	json delete element N of Damage
    	set property `damage` of Vehicle to Damage
        go to ShowDamage
    end
	return

MoveUp:
    put the index of Up into N
    put Damage into Temp
    set Damage to array
    put 0 into M
    while M is less than the json count of Temp
    begin
       	add 1 to M
        if M is less than N
        begin
            take 1 from M
          	append element M of Temp to Damage
        end
        else if M is N
        begin
          	append element M of Temp to Damage
            take 1 from M
          	append element M of Temp to Damage
            add 1 to M
        end
        else
        begin
            take 1 from M
           	append element M of Temp to Damage
        end
       	add 1 to M
    end
    gosub to ShowDamage
    set property `damage` of Vehicle to Damage
	stop

DrawMarker:
    divide the width of Image by 12 giving Width
    divide Width by 2 giving W
    take W from Left giving X
    take W from Top giving Y
    multiply W by 4
    divide W by 3
    put the elements of Marker into M
    add 1 to M
    set the elements of Marker to M
    take 1 from M
    index Marker to M
    create Marker in Container
	set style `display` of Marker to `inline-block`
    set style `position` of Marker to `absolute`
    set style `left` of Marker to X
    set style `top` of Marker to Y
    set style `width` of Marker to Width cat `px`
    set style `height` of Marker to Width cat `px`
    set style `border` of Marker to `2px solid blue`
    set style `border-radius` of Marker to `50%`
    set style `background-color` of Marker to `rgba(` cat Color cat `,0.8)`
    set style `text-align` of Marker to `center`
    set style `font-size` of Marker to W
    set style `font-weight` of Marker to `bold`
    set style `line-height` of Marker to Width cat `px`
    add 1 to N giving M
    if not GotMarker set the content of Marker to M
	return

ClearMarkers:
	put 0 into N
    while N is less than the elements of Marker
    begin
    	index Marker to N
        remove element Marker
    	add 1 to N
    end
    set the elements of Marker to 0
	return

SetSeverityColor:
    if Severity is 0 put YELLOW into Color
    else if Severity is 1 put ORANGE into Color
    else if Severity is 2 put RED into Color
	return