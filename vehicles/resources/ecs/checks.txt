!   General checks
    
    import variable Vehicle
    	and div Part
        and variable Group
        and variable Sections
        and variable Checks
        and input Note

    script Checks
    
    table Table
    tr TR
    td TD
    hr HR
    img Status
    variable Row
    variable Name
    variable N
    variable G
    
    on message go to DoChecks
    set ready
    stop

DoChecks:
    index Part to 0
    create Table in Part
    set the style of Table to `width:100%`
    set the elements of Status to the elements of Checks
    put 0 into Row
    put 0 into G
    while G is less than the json count of Sections
    begin
    	gosub to DoSeparator
        create TR in Table
        create TD in TR
        set the style of TD to `font-weight:bold`
        set the content of TD to element G of Sections
        index Group to G
        put 0 into N
        while N is less than the json count of Group
        begin
        	put element N of Group into Name
            gosub to DoRow
            add 1 to Row
            add 1 to N
        end
    	add 1 to G
    end
    gosub to DoSeparator
    on click Status go to DoClickStatus
	stop

DoRow:
    index Checks to Row
    set Checks to object
    set property `name` of Checks to Name
    set property `check` of Checks to `unchecked`
    create TR in Table
    set style `height` of TR to `3em`
    create TD in TR
    set the content of TD to Name
    create TD in TR
    set the style of TD to `width:7em;text-align:center`
    index Status to Row
    create Status in TD
    set the style of Status to `width:2em;height:2em;margin-left:4em`
    set attribute `src` of Status to `resources/icons/no-entry.png`
    index Note to Row
    create TR in Table
    create TD in TR
    set attribute `colspan` of TD to 2
    create Note in TD
    set the style of Note to `width:100%;display:none`
	return

DoClickStatus:
	put the index of Status into N
    index Checks to N
    index Note to N
    if property `check` of Checks is `unchecked`
    begin
    	set property `check` of Checks to `pass`
    	set attribute `src` of Status to `resources/icons/tick.png`
    	set style `display` of Note to `none`
    end
    else if property `check` of Checks is `pass`
    begin
    	set property `check` of Checks to `warning`
    	set attribute `src` of Status to `resources/icons/exclamation.png`
    	set style `display` of Note to `block`
    end
    else
    begin
    	set property `check` of Checks to `unchecked`
    	set attribute `src` of Status to `resources/icons/no-entry.png`
    	set style `display` of Note to `none`
    end
	stop

DoSeparator:
	create TR in Table
    create TD in TR
	create HR in TD
    create TD in TR
	create HR in TD
    return