!	The HTML editor

	script HTMLEditor

    div Body
    div Container
	div Editor
    div Buttons
	div ScriptName
	input NameEditor
	button Open
	button Save
	button Delete
	span Status
    span Span
	variable Name
	variable Content
	variable Current
	variable N
	variable Password
	variable Valid

!	The media browser
	div Overlay
	div Scroller
	div Media
	div FileListing
	div FileRow
	button CloseButton
	a FileName
	variable Alpha
	variable FileList
	variable FileCount
	variable File

	get Password from storage as `.password`
	if Password is empty put prompt `Please type the admin password` with `` into Password
	rest get Valid from `_verify/` cat Password or alert `Unable to verify password - please try again.`
	if Valid is `yes` put Password into storage as `.password`
  
	create Body
	create Container in Body
	set the style of Container to `width:70%;margin:0 auto;background #ffe`

	create Buttons in Container
	set the style of Buttons to `text-align:center`

	create Open in Buttons
	set the style of Open to `margin-right:0.5em`
	set the text of Open to `Open`
	create Save in Buttons
	set the text of Save to `Save`
	set the style of Save to `margin-right:4em`
	create Delete in Buttons
	set the text of Delete to `Delete`
	create Status in Buttons
	set the style of Status to `position:absolute;float:right;padding-left:2em;color:green`

	create ScriptName in Container
	set the style of ScriptName to `display:flex;margin:0.5em 0`
	create Span in ScriptName
	set the style of Span to `flex:15`
	set the content of Span to `Script name:&nbsp;`
	create NameEditor in ScriptName
	set the style of NameEditor to `flex:85;display:inline-block`
	set the size of NameEditor to 40

	create Editor in Container
	set the style of Editor to `width:100%;height:100%`

	create Overlay in Body
	set the style of Overlay to
		`position:absolute;top:0;left:0;width:100vw;height:100vh;background:rgba(0,0,0,0.0);display:none`

	create Media in Overlay
	set style of Media to `display:none;width:100%;height:100%;text-align:center`

	create FileListing in Media
	set the style of FileListing to
		`display:none;width:50%;height:75%;margin:auto;background-color:white;`
		cat `padding:2em 2em 3em 2em;text-align:center;position: absolute;top: 50%;left: 50%;`
		cat `transform: translateX(-50%) translateY(-50%)`

	create Scroller in FileListing
	set the style of Scroller to `height:100%;overflow:scroll;text-align:left`

	create CloseButton in FileListing
	set the text of CloseButton to `Close` 

	ckeditor attach to Editor
	put empty into Current

	on click Save
	begin
		put the content of NameEditor into Name
		if Name is empty
		begin
			alert `No script name given.`
			stop
		end
		replace ` ` with `_` in Name
		ckeditor get Content from Editor
		if Content is not Current
		begin
			put Content into Current
			rest post Content to `_save/html~` cat Name
			set the content of Status to `Script '` cat Name cat `' saved`
			go to ResetStatus
		end
		else
		begin
			set the content of Status to `Nothing has changed`
			go to ResetStatus
		end
	end

	on click Open go to DoOpen

	on click Delete
	begin
		put the content of NameEditor into Name
		if Name is empty
		begin
			alert `Nothing to delete.`
			stop
		end
		if confirm `Are you sure you want to delete '` cat Name cat `'?`
		begin
			rest post to `_delete/html~` cat Name
			set the content of Status to `Script '` cat Name cat `' deleted`
			set the content of NameEditor to empty
			put empty into Content
			ckeditor set Editor to Content
			put Content into Current
			go to ResetStatus
		end
	end
    
    set ready
	stop

DoOpen:
	ckeditor get Content from Editor
	if Content is not Current
	begin
		if confirm `Content has changed. Do you want to save it?`
		begin
			put the content of NameEditor into Name
			rest post Content to `_save/html~` cat Name
			set the content of Status to `Script '` cat Name cat `' saved`
			fork to ResetStatus
		end
	end

	set style `display` of Overlay to `block`

!	Animate the background
	put 0 into Alpha
	while Alpha is less than 8
	begin
		set style `background-color` of Overlay to `rgba(0,0,0,0.` cat Alpha cat `)`
		wait 4 ticks
		add 1 to Alpha
	end
	wait 20 ticks

!	Make the browser panel visible
	set style `display` of Media to `inline-block`
	set style `display` of FileListing to `inline-block`

! 	Fill the browser with content from the server
	rest get Content from `_list/html`
    decode Content
	put empty into FileList
	put the json count of Content into FileCount
	set the elements of File to FileCount
	set the elements of FileName to FileCount
!	Add a row for each file
	put 0 into N
	while N is less than FileCount
	begin
		index File to N
		index FileName to N
		put `<div id="ec-file-row-INDEX" style="clear:both;padding:0.25em 0;">`
			cat `<a id="ec-file-name-INDEX" href="#"></a></div>` into File
		replace `INDEX` with N in File
		if N is even replace `ODDEVEN` with `ec-even` in File
		else replace `ODDEVEN` with `ec-odd` in File
		put FileList cat File into FileList
		add 1 to N
	end
	set the content of Scroller to FileList
!	Add the document names
	put 0 into N
	while N is less than FileCount
	begin
		index File to N
		index FileName to N
		put element N of Content into File
		attach FileRow to `ec-file-row-` cat N
		attach FileName to `ec-file-name-` cat N
		put property `name` of File into File
		set the content of FileName to File
		if N is even set style `background` of FileRow to `#eee`
		on click FileName go to SelectFile
		add 1 to N
	end
	on click CloseButton go to CloseBrowser
	stop

SelectFile:
	index File to the index of FileName
	set the content of NameEditor to File
	rest get Content from `_load/html~` cat File
    decode Content
	set the content of Status to `Script '` cat File cat `' loaded`
	fork to ResetStatus
	ckeditor set Editor to Content
	put Content into Current

CloseBrowser:
	set style `background-color` of Overlay to `rgba(0,0,0,0.0)`
	set style `display` of Overlay to `none`
	set style `display` of Media to `none`
	stop

ResetStatus:
	wait 2
	set the content of Status to ``
	stop
