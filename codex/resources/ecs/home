!	EasyCoder Home

	script `Home`
    
    div Home
    div Header
    img Banner
    div Content
    div Intro
    div TryIt
    div Indented
    a Button
    hr Rule
    img Editor
    variable Script
    variable UpDown
    variable Value
    variable Color
    variable Codex
    
    create Home
    
    get Codex from storage as `.codex`
    if Codex is `Y` go to Startup

	set the title to `Learn to code with EasyCoder`
    
    if mobile
		set the style of Home to `width:100%`
	else
		set the style of Home to `width: 1200px;margin:0 auto`
    
    create Header in Home
    if mobile
	    set the style of Header to `text-align:center`
	else
		set the style of Header to `margin:0 2em;text-align:center`
    
    create Banner in Header
    set the style of Banner to `width:100%`
    set attribute `src` of Banner to `resources/img/banner.png`
    
    create Rule in Home
    create Content in Home
    
    rest get Script from `_load/html~home`
    
    set the content of Content to Script
    attach Intro to `intro`
    set the style of Intro to `text-align:center;font-size:2.5em`
    
    attach Indented to `indented`
    if not mobile set the style of Indented to `padding:0 5em`
    
    attach TryIt to `tryit`
    set the style of TryIt to `text-align:center;padding:2em`
    attach Button to `button`
    set the style of Button to
    	`border:1px solid black;padding:0.2em 1em;text-decoration:none;border-radius:0.2em`
    if mobile set style `font-size` of Button to `2em`
    else set style `font-size` of Button to `4em`
    
    on click Button go to Startup
    
    attach Editor to `editor`
    set the style of Editor to `margin-top:1em;width:100%`
    on click Editor go to Startup
    
! Animate the button
	wait 1
    set UpDown
    put 255 into Value
    while true
    begin
        wait 1 tick
        multiply Value by 256 giving Color
        add Value to Color
        multiply Color by 256
        add 255 to Color
        set style `background-color` of Button to color Color
        if UpDown
        begin
            if Value is 255 clear UpDown
            else add 1 to Value
        end
        else
        begin
            if Value is 128 set UpDown
            else take 1 from Value
        end
    end

Startup:
    put `Y` into storage as `.codex`
    load plugin `codemirror`
    clear Home
    rest get Script from `_load/ecs~codex`
    run Script
	wait 2