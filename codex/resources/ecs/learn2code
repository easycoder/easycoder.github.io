!	Learn To Code

    script LearnToCode

	div Body
    div ScriptPanel
    div CodePanel
    div NonCodePanel
    div RunPanel
    div HelpOuter
    div HelpPanel
    div ReferencePanel
    div Container
    div Controls
    div Buttons
    div HelpButtons
    div ScriptName
    div ContentDiv
    div Clear
    div Code
    div Tracer
    input NameEditor
    textarea ContentEditor
    span Status
    span Span
    img New
    img Open
    img Save
    img RunStop
    img Delete
    img Cycle
    img Back
    img Forward
    img Banner
    img Reference
    a Copy
    a Next
    a Link
    module TestModule
    variable Password
    variable PasswordRequested
    variable PasswordValid
    variable Name
    variable CurrentName
    variable Content
    variable Current
    variable Script
    variable Fragment
    variable Mobile
    variable Running
    variable View
    variable Step
    variable Message
    variable Exists
    variable ShowRun

  ! The browser
    div Overlay
    div Scroller
    div Media
    div FileListing
    div FileRow
    div LowerPanel
    button CloseButton
    a FileName
    variable Alpha
    variable FileList
    variable FileCount
    variable File
    variable Files
    variable N
    variable FileIsOpen
    variable Item

    clear PasswordRequested
    
    clear Mobile
    if portrait
    begin
    	if mobile set Mobile else clear Mobile
    end
    set ShowRun

    load plugin `svg`
    
    codemirror init basic profile `/easycoder/plugins/codemirror-ecs.js`
    require css `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.css`
    require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/dialog/dialog.js`
    require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/search.js`
    require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/searchcursor.js`
    require js `https://cdnjs.cloudflare.com/ajax/libs/codemirror/5.46.0/addon/search/jump-to-line.js`
    
    set the title to `EasyCoder Tutorials`

    get Step from storage as `.step`
    if Step is empty
    begin
    	put 0 into Step
        put Step into storage as `.step`
    end
    
    create Body
    if Mobile
	    set the style of Body to `width:100%;height:100%`
    else
	    set the style of Body to `width:100%;height:100%;display:flex`

    create ScriptPanel in Body    
    create Container in ScriptPanel
	set the style of Container to `width:100%;height:100%;display:flex;flex-direction: column`
    
    create Controls in Container
    set the style of Controls to `flex:5em`

    create Buttons in Controls
    set the style of Buttons to `width:100%;padding:0.5em`

    create Link in Buttons
    create New in Link
    set the style of New to `width:40px;margin-right:0.5em`
    set attribute `src` of New to `resources/icon/new.png`
    set attribute `title` of New to `New`
    create Open in Link
    set the style of Open to `width:40px;margin-right:0.5em`
    set attribute `src` of Open to `resources/icon/open.png`
    set attribute `title` of Open to `Open`
    create Link in Buttons
    create Save in Link
    set the style of Save to `width:40px;margin-right:1.5em`
    set attribute `src` of Save to `resources/icon/save.png`
    set attribute `title` of Save to `Save`
    create Link in Buttons
    create Delete in Link
    set the style of Delete to `width:40px;margin-right:1.5em`
    set attribute `src` of Delete to `resources/icon/trash.png`
    set attribute `title` of Delete to `Delete`
    create Link in Buttons
    create RunStop in Link
    set the style of RunStop to `width:40px;margin-right:1.5em`
    set attribute `src` of RunStop to `resources/icon/run.png`
    set attribute `title` of RunStop to `Run`
   	create Link in Buttons
   	create Cycle in Link
   	set the style of Cycle to `width:40px`
   	set attribute `src` of Cycle to `resources/icon/cycle.png`
   	set attribute `title` of Cycle to `Cycle screens`

    create Status in Buttons
    if Mobile set the style of Status to `height:1em;color:green`
    else set the style of Status to `float:right;margin:0.5em 2em 0 0;color:green`

    create ScriptName in Controls
    set the style of ScriptName to `display:flex;margin:0.5em 0;padding:0.5em`
    create Span in ScriptName
    set the style of Span to `flex:15`
    set the content of Span to `Script&nbsp;name:&nbsp;`
    create NameEditor in ScriptName
    set the style of NameEditor to `flex:85;display:inline-block`

	create ContentDiv in Container
    set the style of ContentDiv to `flex:1`
    if Mobile
    begin
    	set the style of ContentDiv to
        	`position:relative;width:100%;height:100%;overflow-x:scroll;overflow-y:hidden`
	end
    else
    begin
    	set the style of ContentDiv to `width:100%;height:100%;overflow-x:scroll;overflow-y:hidden`
	end
    
    if Mobile
    begin
    	create ContentEditor in ContentDiv
    	set the style of ContentEditor to `width:100%;height:100%;border:none`
	end
    else
    begin
    	create ContentEditor in ContentDiv
    	set the style of ContentEditor to `width:100%;border:none`
	end
    
	codemirror attach to ContentEditor
    set FileIsOpen

    if Mobile
    begin
        set the style of CodePanel to `display:none`
		set the style of ScriptPanel to `width:100%;height:100%`
        create NonCodePanel in ContentDiv
	    create RunPanel in NonCodePanel
    	set the style of RunPanel to `display:none`

		create HelpOuter in NonCodePanel
        set the style of HelpOuter to `width:100%;height:100%;overflow-y:scroll`
    end
    else
    begin
		set the style of ScriptPanel to `flex:50;height:100%;overflow:hidden;border:1px solid gray`
	    create RunPanel in Body
    	set the style of RunPanel to `display:none;flex:50;margin-left:1em;border:1px solid gray`

		create HelpOuter in Body
    	set the style of HelpOuter to
        	`flex:50;margin-left:1em;border:1px solid gray;padding:0 0.5em;overflow-y:scroll`
    end

	create Tracer in HelpOuter
    set attribute `id` of Tracer to `easycoder-tracer`
    create Banner in HelpOuter
    set the style of Banner to `width:100%`
    set attribute `src` of Banner to `resources/img/banner.png`
    
    create HelpButtons in HelpOuter
    set the style of HelpButtons to `text-align:center`
    create Link in HelpButtons
    create Back in Link
       set the style of Back to `width:40px`
    set attribute `src` of Back to `resources/icon/arrow-back.png`
    set attribute `title` of Back to `Previous step`
    create Link in HelpButtons
    create Forward in Link
       set the style of Forward to `margin-left:2em;width:40px`
    set attribute `src` of Forward to `resources/icon/arrow-forward.png`
    set attribute `title` of Forward to `Next step`
    create Link in HelpButtons
    create Reference in Link
    set the style of Reference to `margin-left:4em;width:40px`
    set attribute `src` of Reference to `resources/icon/book.png`
    set attribute `title` of Reference to `Programmer's Reference`

	create HelpPanel in HelpOuter
    set the style of HelpPanel to `display:none;width:100%;height:100%;line-height:1.5em`
	create ReferencePanel in HelpOuter
    set the style of ReferencePanel to `display:none;width:100%;height:100%;line-height:1.5em`

	gosub to ShowHelpPage
	rest get Script from `_load/ecs~docman`
    run Script with ReferencePanel

	get Item from storage as `.ref`
    if Item
    begin
	    set style `display` of ReferencePanel to `block`
        set style `display` of HelpButtons to `none`
    end
    else set style `display` of HelpPanel to `block`

	on click Back go to StepBack
	on click Forward go to StepForward
	on click Reference
    begin
	    set style `display` of ReferencePanel to `block`
        set style `display` of HelpPanel to `none`
        set style `display` of HelpButtons to `none`
        put `y` into storage as `.ref`
    end
    
    on message
    begin
    	put the message into Message
    	if Message is `next` go to StepForward
        if Message is `tutorial`
        begin
		    set style `display` of ReferencePanel to `none`
	        set style `display` of HelpPanel to `block`
	        set style `display` of HelpButtons to `block`
	        put empty into storage as `.ref`
        end
    end
    
    on error
    begin
    	gosub to StopTestModule
        clear Running
        set attribute `src` of RunStop to `resources/icon/run.png`
    end
   
    create Overlay in Body
    set the style of Overlay to
      `position:absolute;top:0;left:0;width:100vw;height:100vh;background:rgba(0,0,0,0.0);display:none`

    create Media in Overlay
    set style of Media to `display:none;width:100%;height:100%;text-align:center`

    create FileListing in Media
    set the style of FileListing to
      `display:none;width:50%;height:75%;margin:auto;background-color:white;`
      cat `padding:2em 2em 3em 2em;text-align:center;position: absolute;top: 50%;left: 50%;`
      cat `transform: translateX(-50%) translateY(-50%)`

    create Scroller in FileListing
    set the style of Scroller to `height:100%;overflow:scroll;text-align:left`
    
    create LowerPanel in FileListing
    
    create CloseButton in LowerPanel
    set the style of CloseButton to `margin-left:2em`
    set the text of CloseButton to `Close`

    put empty into Current
    
    on click New
    begin
    	gosub to StopTestModule
    	if Mobile
        begin
	    	put `code` into View
        	gosub to SetView
		end
        codemirror close ContentEditor
    	put the content of ContentEditor into Content
    	if Content is not Current
    	begin
			if confirm `Content has changed. Do you want to save it?`
			begin
      			put the content of NameEditor into Name
      			if Name is empty
      			begin
        			set the content of Status to `No script name has been given`
        			go to ResetStatus
      			end
    	  		if PasswordValid rest post Content to `_save/scripts~` cat Name
      			else put Content into storage as CurrentName
            end
    	end
    	clear FileIsOpen
        set the content of ContentEditor to empty
      	codemirror attach to ContentEditor
        set the content of NameEditor to empty
        put empty into Content
        put Content into Current
	end

    on click Save
    begin
    	gosub to GetPassword
        put the content of NameEditor into Name
        if Name is empty
        begin
            set the content of Status to `No script name has been given`
            go to ResetStatus
        end
        replace ` ` with `_` in Name
        codemirror close ContentEditor
        put the content of ContentEditor into Content
        codemirror attach to ContentEditor
        if Content is not Current
        begin
            if PasswordValid rest post Content to `_save/scripts~` cat Name
            else put Content into storage as Name
            put Content into Current
            set the content of Status to `Script '` cat Name cat `' saved`
            fork to ResetStatus
        end
        else
        begin
            set the content of Status to `Nothing has changed`
            fork to ResetStatus
        end
    end
    
    on click Delete
    begin
    	gosub to GetPassword
        put the content of NameEditor into Name
        if Name is empty
        begin
            alert `Nothing to delete.`
            stop
        end
        if confirm `Are you sure you want to delete "` cat Name cat `"?`
        begin
            codemirror close ContentEditor
            set the content of ContentEditor to empty
            codemirror attach to ContentEditor
            if PasswordValid rest post Content to `_delete/scripts~` cat Name
            else remove Name from storage
            set the content of Status to `Script "` cat Name cat `" deleted`
            set the content of NameEditor to empty
            put empty into Content
            put Content into Current
            go to ResetStatus
        end
    end
    
    clear Running
    put `help` into View

    on click Open go to DoOpen
    on click RunStop go to DoRunStop
    
    on click Cycle
    begin
    	if Mobile
        begin
    		if View is `help` put `code` into View
        	else if View is `code`
        	begin
        		if Running put `run` into View
            	else put `help` into View
        	end
        	else if View is `run` put `help` into View
        	goto SetView
        end
        else
        begin
    		if View is `help`
            begin
            	if Running
                begin
	            	set style `display` of RunPanel to `block`
    	        	set style `display` of HelpOuter to `none`
        	    	put `run` into View
                end
            end
        	else
            begin
            	set style `display` of RunPanel to `none`
            	set style `display` of HelpOuter to `block`
            	put `help` into View
            end
        end
    end
    stop

DoOpen:
	gosub to StopTestModule
    gosub to GetPassword

    if Mobile
    begin
    	put `code` into View
    	gosub to SetView
    end
	codemirror close ContentEditor
    clear FileIsOpen
    put the content of ContentEditor into Content
    if Content is not Current
    begin
      if confirm `Content has changed. Do you want to save it?`
      begin
      	if PasswordValid rest post Content to `_save/scripts~` cat Name
      	else put Content into storage as Name
      end
    end

  ! Animate the background
    set style `display` of Overlay to `block`
    put 0 into Alpha
    while Alpha is less than 8
    begin
      set style `background-color` of Overlay to `rgba(0,0,0,0.` cat Alpha cat `)`
      wait 4 ticks
      add 1 to Alpha
    end
    wait 10 ticks

  ! Make the browser panel visible
    set style `display` of Media to `block`
    set style `display` of FileListing to `inline-block`

  ! Fill the browser with content from the server
    if PasswordValid
    begin
        rest get Files from `_list/scripts`
        put the json count of Files into FileCount
        put empty into Content
        put 0 into N
        while N is less than FileCount
        begin
            put element N of Files into Item
            if property `type` of Item is `file` json add property `name` of Item to Content
            add 1 to N
        end
    end
    else
    begin
    	get Files from storage
	  	put the json count of Files into FileCount
	  	put empty into Content
	  	put 0 into N
	  	while N is less than FileCount
	  	begin
	  		put element N of Files into Item
	    	if left 1 of Item is not `.` json add Item to Content
	    	add 1 to N
	  	end
    end
    json sort Content
    put empty into FileList
    put the json count of Content into FileCount
    set the elements of File to FileCount
    set the elements of FileName to FileCount
  ! Add a row for each file
    put 0 into N
    while N is less than FileCount
    begin
      index File to N
      index FileName to N
      put `<div id="ec-file-row-INDEX" style="clear:both;padding:0.25em 0;">`
        cat `<a id="ec-file-name-INDEX" href="#"></a></div>` into File
      replace `INDEX` with N in File
      if N is even replace `ODDEVEN` with `ec-even` in File
      else replace `ODDEVEN` with `ec-odd` in File
      put FileList cat File into FileList
      add 1 to N
    end

    set the content of Scroller to FileList
  ! Add the document names
    put 0 into N
    while N is less than FileCount
    begin
      index File to N
      index FileName to N
      put element N of Content into File
      attach FileRow to `ec-file-row-` cat N
      attach FileName to `ec-file-name-` cat N
      set the content of FileName to File
      if N is even set style `background` of FileRow to `lightgray`
      on click FileName go to SelectFile
      add 1 to N
    end
    on click CloseButton
    begin
      put Current into Content
      go to CloseBrowser
    end
    stop
    
SelectFile:
    index File to the index of FileName
    set the content of NameEditor to File
	if PasswordValid rest get Content from `_load/scripts~` cat File
    else get Content from storage as File
    put Content into Current
    set the content of Status to `Script '` cat File cat `' loaded`
    fork to ResetStatus
    set the title to `Script Editor - ` cat File
    set ShowRun

CloseBrowser:
    set style `background-color` of Overlay to `rgba(0,0,0,0.0)`
    set style `display` of Overlay to `none`
    set style `display` of Media to `none`
    codemirror attach to ContentEditor
    codemirror set content of ContentEditor to Content
    stop

SetView:
	if View is `code`
    begin
    	set style `display` of CodePanel to `block`
    	set style `display` of NonCodePanel to `none`
    end
	else if View is `run`
    begin        
    	set style `display` of CodePanel to `none`
    	set style `display` of NonCodePanel to `block`
    	set style `display` of RunPanel to `block`
    	set style `display` of HelpOuter to `none`
    end
	else if View is `help`
    begin     
    	set style `display` of CodePanel to `none`
    	set style `display` of NonCodePanel to `block`
    	set style `display` of RunPanel to `none`
    	set style `display` of HelpOuter to `block`
    end
	return

ResetStatus:
    wait 2
    set the content of Status to ``
    stop

ShowHelpPage:
	put Step into storage as `.step`
    rest get Script from `_load/html~step` cat Step
    set the content of HelpPanel to Script

	attach Code to `code`
    set the style of Code to `font-family:mono;color:#660000;margin-bottom:1em`
    rest get Fragment from `_load/ecs~fragment~step` cat Step
    put Fragment into Content
    replace newline with `<br>` in Content
    replace ` ` with `&nbsp;` in Content
    set the content of Code to Content
    
    if Script is not empty
    begin
	    attach Copy to `copy`
	    set the style of Copy to
	    	`border:1px solid black;border-radius:0.5em;padding:0.3em;background:lightgray;text-decoration:none`
	    on click Copy
	    begin
        	gosub to StopTestModule
        	put `code` into View
            if Mobile gosub to SetView
            codemirror close ContentEditor
    		codemirror attach to ContentEditor
    		codemirror set content of ContentEditor to Fragment
            set the text of NameEditor to empty
            attach Clear to `clear`
            if the content of Clear is `y` set ShowRun else clear ShowRun
 	   end
    end

    attach Next to `next`
    on click Next
    begin
    	add 1 to Step
        goto ShowHelpPage
    end
    scroll HelpOuter to 0
	return

StepBack:
	if Step is 0 stop
	clear HelpPanel
	take 1 from Step
    gosub to ShowHelpPage
	stop

StepForward:
	add 1 to Step
    put `html~step` cat Step into Name
    rest get Exists from `_exists/` cat Name
    if Exists is `Y` gosub to ShowHelpPage
    else take 1 from Step
	stop
    
DoRunStop:
    if Running
    begin
    	gosub to StopTestModule
        stop
    end

    codemirror close ContentEditor
    put the content of ContentEditor into Script
    codemirror attach to ContentEditor

    if Script is empty
    begin
    	alert `Nothing to run`
    	stop
    end
    
	put `run` into View
	if Mobile
    begin
    	gosub to SetView
    end
    else if ShowRun
    begin
    	set style `display` of HelpOuter to `none`
        set style `display` of RunPanel to `block`
	end
    
    set attribute `src` of RunStop to `resources/icon/runstop.png`
    set Running
    wait 10 ticks
    
    run Script with RunPanel as TestModule nowait then
    begin
        clear Running
    	set attribute `src` of RunStop to `resources/icon/run.png`
        set style `display` of RunPanel to `none`
        set style `display` of HelpOuter to `block`
        put `help` into View
	end
    stop

StopTestModule:
	if TestModule is running
    begin
    	stop TestModule
        clear Running
        clear RunPanel
        if Mobile
        begin
        	put `code` into View
            gosub to SetView
        	codemirror close ContentEditor
    		codemirror attach to ContentEditor
        end
        else put `help` into View
        set style `display` of HelpOuter to `block`
        set attribute `src` of RunStop to `resources/icon/run.png`
    end
	return

GetPassword:
	if not PasswordRequested
    begin
	    set PasswordRequested
        if hostname is `localhost` goto SetPasswordValid
        if hostname is `127.0.0.1` goto SetPasswordValid
        get Password from storage as `.password`
	    if Password is empty
        begin
        put `Please provide the admin password` cat newline
            cat `or click OK to use private browser storage.` into Message
        	put prompt Message with `` into Password
        end
        rest get PasswordValid from `_verify/` cat Password
        if PasswordValid is `yes` 
        begin
            put Password into storage as `.password`
        	set PasswordValid
	    end
        else clear PasswordValid
	end
    return

SetPasswordValid:
	set PasswordValid
    return