!   Record viewer

    script Viewer
    
    import variable ID
    
    div Body
    div Container
    div Panel
    span Label
    img Top
    img Mid
    img Bottom
    img Search
    img Edit
    h1 Title
    p Para
    a ChildLink
    a SiblingLink
    a Link
    variable ReadOnly
    variable Index
    variable Map
    variable Record
    variable Record2
    variable Item
    variable GivenName
    variable OtherNames
    variable FamilyName
    variable DateOfBirth
    variable PlaceOfBirth
    variable Date
    variable FID
    variable MID
    variable Father
    variable Mother
    variable Child
    variable Sibling
    variable Months
    variable Month
    variable Gender
    variable Message
    variable List
    variable Text
    variable N
    
    clear ReadOnly
    rest post now to `save/timestamp`
    or begin
    	set ReadOnly
        go to Continue
    end

Continue:
    rest get Index from `/index.json`
    rest get Map from `/map.json`

clear Months
    json add `January` to Months
    json add `February` to Months
    json add `March` to Months
    json add `April` to Months
    json add `May` to Months
    json add `June` to Months
    json add `July` to Months
    json add `August` to Months
    json add `September` to Months
    json add `October` to Months
    json add `November` to Months
    json add `December` to Months
    
    clear body
    create Body in body
    set the style of Body to `position:relative;width:calc(100vw - 20px);height:calc(100vh - 20px)`
    
    create Mid in Body
    set the style of Mid to `width:100%;height:100%;position:relative`
    set attribute `src` of Mid to `images/parchment-mid.png`
    
    create Top in Body
    set the style of Top to `width:100%;height:10vh;position:absolute;left:0;top:0`
    set attribute `src` of Top to `images/parchment-top.png`
    
    create Bottom in Body
    set the style of Bottom to `width:100%;height:10vh;position:absolute;left:0;bottom:0`
    set attribute `src` of Bottom to `images/parchment-bottom.png`
    
    create Container in Body
    set the style of Container to
    	`width:calc(80vw - 20px);height:calc(80vh - 20px);position:absolute;top:10vh;left:10vw`
    
ViewRecord:
    rest get Record from `/map/` cat ID cat `.json` or stop
    
    put property `fname` of Record into FamilyName
    put property `oname` of Record into OtherNames
    put property `gname` of Record into GivenName
    put property `dob` of Record into DateOfBirth
    put property `pob` of Record into PlaceOfBirth
    
    create Panel in Container
    set the style of Panel to `width:calc(100% - 2em);height:calc(100% - 2em);`
    	cat `padding:1em;border:1px solid #95673f`

    set style `font-size` of Panel to `2vh`
    create Title in Panel
    set the style of Title to `text-align:center;margin-top:0`
    put GivenName into Text
    if OtherNames is not empty put Text cat ` ` cat OtherNames into Text
    put Text cat ` ` cat FamilyName into Text
    set the content of Title to Text
    
    create Para in Panel
    put DateOfBirth into Date
    gosub to ExpandDate
    if PlaceOfBirth
    begin
    	create Label in Panel
    	set the content of Label to PlaceOfBirth cat `, ` cat Date
        if Date
        begin
          	create Label in Panel
          	set the content of Label to `, ` cat Date
       	end
    end
    else
    begin
    	if Date
        begin
        	create Label in Panel
        	set the content of Label to Date
        end
    end
    
    put property `father` of Record into FID
    if not FID go to VR2
    rest get Record2 from `/map/` cat FID cat `.json` or go to VR2
    put property `gname` of Record2 into GivenName
    put property `oname` of Record2 into OtherNames
    put property `fname` of Record2 into FamilyName
    put GivenName into Father
    if OtherNames is not empty
    begin
    	if Father is not empty put Father cat ` ` into Father
    	put Father cat OtherNames into Father
    end
    if FamilyName is not empty
    begin
    	if Father is not empty put Father cat ` ` into Father
    	put Father cat FamilyName into Father
    end

VR2:
    put property `mother` of Record into MID
    if not MID go to VR3
    rest get Record2 from `/map/` cat MID cat `.json` or go to VR3
    put property `gname` of Record2 into GivenName
    put property `oname` of Record2 into OtherNames
    put property `fname` of Record2 into FamilyName
    put GivenName into Mother
    if OtherNames is not empty
    begin
    	if Mother is not empty put Mother cat ` ` into Mother
    	put Mother cat OtherNames into Mother
    end
    if FamilyName is not empty
    begin
    	if Mother is not empty put Mother cat ` ` into Mother
    	put Mother cat FamilyName into Mother
    end

VR3:
	if Father cat Mother is empty go to VR4
    create Para in Panel
    if Father is not empty
    begin
        create Label in Para
        set the content of Label to `Father: `
    	create Link in Para
        set the content of Link to Father
        on click Link
        begin
        	put FID into ID
            go to ViewAnotherRecord
        end
    end
    if Mother is not empty
    begin
    	if Father is not empty
        begin
	        create Label in Para
            set the content of Label to break
        end
        create Label in Para
        set the content of Label to `Mother: `
    	create Link in Para
        set the content of Link to Mother
        on click Link
        begin
        	put MID into ID
            go to ViewAnotherRecord
        end
    end

VR4:
	put property ID of Map into Item
    put property `children` of Item into List
    if the json count of List is not 0
    begin
    	set the elements of Child to the json count of List
    	set the elements of ChildLink to the json count of List
    	create Para in Panel
	    put 0 into N
	    while N is less than the json count of List
	    begin
        	if N is not 0
            begin
                create Label in Para
                set the content of Label to break
            end
        	index Child to N
        	index ChildLink to N
	    	put element N of List into Child
	        rest get Record2 from `/map/` cat Child cat `.json` or go to VR5
	        put property `gname` of Record2 into GivenName
	        put property `fname` of Record2 into FamilyName
	        put property `gender` of Record2 into Gender
            create Label in Para
            if Gender is `m` set the content of Label to  `Son: `
            else if Gender is `f` set the content of Label to `Daughter: `
            else set the content of Label to `Child: `
            create ChildLink in Para
            set the content of ChildLink to GivenName cat ` ` cat FamilyName
            on click ChildLink
            begin
            	index Child to the index of ChildLink
                put Child into ID
                go to ViewAnotherRecord
            end
	    	add 1 to N
	    end
    end

VR5:
	put property ID of Map into Item
    put property `siblings` of Item into List
    if the json count of List is not 0
    begin
    	set the elements of Sibling to the json count of List
    	set the elements of SiblingLink to the json count of List
    	create Para in Panel
	    put 0 into N
	    while N is less than the json count of List
	    begin
        	index Sibling to N
        	index SiblingLink to N
	    	put element N of List into Sibling
	        rest get Record2 from `/map/` cat Sibling cat `.json` or go to VR6
	        put property `gname` of Record2 into GivenName
	        put property `fname` of Record2 into FamilyName
	        put property `gender` of Record2 into Gender
            create Label in Para
            if Gender is `m` set the content of Label to `Brother: `
            else if Gender is `f` set the content of Label to `Sister: `
            else set the content of Label to `Sibling: `
            create SiblingLink in Para
            set the content of SiblingLink to GivenName cat ` ` cat FamilyName
            on click SiblingLink
            begin
            	index Sibling to the index of SiblingLink
                put Sibling into ID
                go to ViewAnotherRecord
            end
	    	add 1 to N
	    end
    end

VR6:
	create Search in Body
    set the style of Search to `width:5vw;height:5vw;position:absolute;top:11vh;right:11vw`
    set attribute `src` of Search to `icons/binoculars.png`
    on click Search
    begin
    	clear Message
        set property `action` of Message to `find`
        send Message to parent
    	exit
    end

	if not ReadOnly
    begin
		create Edit in Body
	    set the style of Edit to `width:5vw;height:5vw;position:absolute;top:11vh;right:17vw`
	    set attribute `src` of Edit to `icons/edit.png`
	    on click Edit
	    begin
	    	clear Message
	        set property `action` of Message to `edit`
	        set property `id` of Message to ID
	        send Message to parent
	        exit
	    end
    end

	set ready
    stop

ExpandDate:
	put Date into Text
    put the value of from 5 to 7 of Text into Month
    take 1 from Month
    put element Month of Months into Date
    put Date cat ` ` cat from 8 of Text into Date
    put Date cat ` ` cat left 4 of Text into Date
    return

ViewAnotherRecord:
    clear Container
    clear Father
    clear Mother
    go to ViewRecord
