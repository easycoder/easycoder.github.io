!   Parents

    script Parents
    
    import div Container
    	and variable MainRecord
    
	div Row
    div Label
    div Choices
    input Parent
    button FindButton
    button ChoiceButton
    variable Index
    variable Key
    variable ParentsRecord
    variable ChoiceIndex
    variable Selection
    variable ID
    variable Text
    variable Message
    variable N
    variable F
    variable G
    variable D
    
    set the elements of Key to 2
    set the elements of Parent to 2
    set the elements of FindButton to 2
    rest get Index from `index.json`
    
    index Key to 0
    put `father` into Key
    create Row in Container
    set the class of Row to `rowstyle`
	create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Father:`
    index Parent to 0
    create Parent in Row
    set the class of Parent to `inputstyle`
    index FindButton to 0
    create FindButton in Row
    set the style of FindButton to `width:5em`
    set the text of FindButton to `Find`

    index Key to 1
    put `mother` into Key
    create Row in Container
    set the class of Row to `rowstyle`
	create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Mother:`
    index Parent to 1
    create Parent in Row
    set the class of Parent to `inputstyle`
    index FindButton to 1
    create FindButton in Row
    set the style of FindButton to `width:5em`
    set the text of FindButton to `Find`
    
    create Choices in Container
    set the style of Choices to `display:none`
    
    on click FindButton go to FindParent
    
    on message
    begin
    	put the message into Message
    	if Message is `exit` exit
        if Message is `fill`
        begin
        	put property `father` of MainRecord into ID
            if ID is not 0
            begin
    			rest get ParentsRecord from `/map/` cat ID cat `.json`
                or begin
                	alert `Record ` cat ID cat ` not found`
                    stop
                end
            	put property `gname` of ParentsRecord into G
            	put property `fname` of ParentsRecord into F
            	index Parent to 0
            	set the content of Parent to G cat ` ` cat F
            end
        	put property `mother` of MainRecord into ID
            if ID is not 0
            begin
	    		rest get ParentsRecord from `/map/` cat ID cat `.json`
                or begin
                	alert `Record ` cat ID cat ` not found`
                    stop
                end
	            put property `gname` of ParentsRecord into G
	            put property `fname` of ParentsRecord into F
	            index Parent to 1
	            set the content of Parent to G cat ` ` cat F
            end
        end
    end
    
    set ready
    stop

FindParent:
    set style `display` of Choices to `none`
	index Parent to the index of FindButton
    put `[]` into Selection
    put 0 into N
    while N is less than the json count of Index
    begin
    	put element N of Index into ID
    	rest get ParentsRecord from `/map/` cat ID cat `.json`
        put property `fname` of ParentsRecord into F
        put property `gname` of ParentsRecord into G
        if Parent is empty go to MissingData
        if Parent is G cat ` ` cat F
        begin
        	json add N to Selection
            go to FP2
        end
        if the position nocase of Parent in F is 0 json add ID to Selection
        else if the position nocase of Parent in G is 0 json add ID to Selection
FP2:
    	add 1 to N
    end
    put the json count of Selection into N
    if N is greater than 0
    begin
    	if N is 1
        begin
        	put element 0 of Selection into ID
            go to Chosen
        end
        ! Show a list of matching results
        clear Choices
        set the elements of ChoiceButton to the json count of Selection
        set the elements of ChoiceIndex to the json count of Selection
        put 0 into N
        while N is less than the json count of Selection
        begin
        	put element N of Selection into ID
    		rest get ParentsRecord from `/map/` cat ID cat `.json`
            put property `fname` of ParentsRecord into F
            put property `gname` of ParentsRecord into G
            put property `dob` of ParentsRecord into D
            create Row in Choices
            set the class of Row to `rowstyle`
            create Label in Row
            set the class of Label to `labelstyle`
            index ChoiceButton to N
            create ChoiceButton in Row
            put G cat ` ` cat F into Text
            if D is not empty put Text cat ` (` cat D cat `)` into Text
            set the text of ChoiceButton to Text
            index ChoiceIndex to N
           	put ID into ChoiceIndex
        	add 1 to N
        end
        set style `display` of Choices to `block`
        on click ChoiceButton
        begin
	       	put the index of ChoiceButton into N
            index ChoiceIndex to N
        	put ChoiceIndex into ID
        	set style `display` of Choices to `none`
            go to Chosen
        end
       	stop
    end
    alert `No matching record(s) found.`
	stop

Chosen:
    rest get ParentsRecord from `/map/` cat ID cat `.json`
    put property `gname` of ParentsRecord into G
    put property `fname` of ParentsRecord into F
    set the content of Parent to G cat ` ` cat F
    index Key to the index of Parent
    set property Key of MainRecord to ID
    stop

MissingData:
	alert `Please provide part or all of either the given or family names.`
    stop